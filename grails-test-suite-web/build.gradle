configurations.all {
    resolutionStrategy {
        preferProjectModules()
    }
}

configurations.testCompileClasspath {
    exclude module: "grails-plugin-testing"
}

dependencies {

    testImplementation project(':grails-test-suite-base'),
                project(':grails-plugin-domain-class'),
                project(':grails-plugin-codecs'),
                project(':grails-plugin-datasource'),
                project(':grails-plugin-i18n'),
                project(':grails-plugin-url-mappings'),
                project(':grails-plugin-databinding'),
                project(':grails-plugin-services'),
                project(':grails-plugin-interceptors'),
                project(':grails-plugin-controllers'),
                project(':grails-plugin-rest'),
                project(':grails-web'),
                project(':grails-web-databinding'),
                project(':grails-spring')

    testImplementation "org.grails.plugins:converters"
    testImplementation "org.grails:grails-datastore-gorm-hibernate5"
    testImplementation "org.grails:grails-web-testing-support"
    testImplementation "org.grails.plugins:gsp"
    testImplementation "org.grails.plugins:async"
    testImplementation "org.grails:grails-gorm-testing-support"
    testImplementation "com.fasterxml.jackson.core:jackson-databind"

    testRuntimeOnly "org.springframework:spring-aspects"
    testRuntimeOnly "org.aspectj:aspectjrt", "org.aspectj:aspectjweaver"

}

// javaee-web-api has a bad versions of classes we need to compile against
// Just remove it fromt the compile classpath here
configurations {
    testCompileClasspath {
        exclude module: "javaee-web-api"
    }
}

compileTestGroovy {
    groovyOptions.listFiles = true
}

def defaultTestConfig = {
    maxParallelForks = isCiBuild ? 2 : 4
    forkEvery = isCiBuild ? 10 : 100
    excludes = ["**/*TestCase.class",
                "**/*\$*.class"]
}

def isolatedTests = [
    "**/ContentFormatControllerTests.class",
    "**/JSONBindingTests.class",
    "**/AutoParams*MarshallingTests.class",
    "**/JSONBindingToNullTests.class",
    "**/ControllerWithXmlConvertersTests.class",
    "**/GroovyPageAttributesTests.class",
    "**/BindingExcludeTests.class",
    "**/NestedXmlBindingTests.class",
    "**/GSPResponseWriterSpec.class",
    "**/RespondMethodSpec.class",
    "**/ContentNegotiationSpec.class",
    "**/pages/ext/jsp/*.class",
    "**/WithFormatContentTypeSpec.class",
    "**/CommandObjectNoDataSpec.class"
]

task execIsolatedTests(type: Test) {
    configure defaultTestConfig
    forkEvery = 1
    includes = isolatedTests
}

task createCombinedReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn execIsolatedTests, test
}

test {
    configure defaultTestConfig
    excludes += isolatedTests
}

test.dependsOn execIsolatedTests
