/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.web.mapping.mvc

import groovy.transform.CompileStatic

import jakarta.servlet.http.HttpServletRequest
import jakarta.servlet.http.HttpServletResponse

import org.springframework.beans.factory.annotation.Autowired
import org.springframework.util.Assert
import org.springframework.web.context.request.WebRequestInterceptor
import org.springframework.web.filter.ServerHttpObservationFilter
import org.springframework.web.servlet.HandlerExecutionChain
import org.springframework.web.servlet.HandlerInterceptor
import org.springframework.web.servlet.ModelAndView
import org.springframework.web.servlet.handler.AbstractHandlerMapping
import org.springframework.web.servlet.handler.MappedInterceptor
import org.springframework.web.servlet.handler.WebRequestHandlerInterceptorAdapter
import org.springframework.web.util.UrlPathHelper

import grails.web.http.HttpHeaders
import grails.web.mapping.UrlMapping
import grails.web.mapping.UrlMappingInfo
import grails.web.mapping.UrlMappingsHolder
import grails.web.mapping.cors.GrailsCorsConfiguration
import grails.web.mime.MimeType
import grails.web.mime.MimeTypeResolver
import org.grails.exceptions.ExceptionUtils
import org.grails.web.servlet.mvc.GrailsWebRequest
import org.grails.web.util.GrailsApplicationAttributes
import org.grails.web.util.HiddenHttpMethod
import org.grails.web.util.WebUtils

/**
 *
 * Spring MVC {@link org.springframework.web.servlet.HandlerMapping} to match requests onto Grails controllers
 *
 * @since 3.0
 */
@CompileStatic
class UrlMappingsHandlerMapping extends AbstractHandlerMapping {

    public static final String MATCHED_REQUEST = 'org.grails.url.match.info'

    // Both are stateless, so one shared instance each rather than two allocations per request.
    private static final HandlerInterceptor OBSERVATION_ROUTE_HANDLER = new ObservationRouteHandler()
    private static final HandlerInterceptor ERROR_HANDLING_HANDLER = new ErrorHandlingHandler()

    /**
     * Whether to resolve a "_method" parameter on a POST into the overridden request method while matching
     * URL mappings. Set when the hidden HTTP method filter is disabled. The dispatcher normally wraps the
     * request with the override before this runs, so this is the fallback for one that does not.
     */
    boolean resolveHiddenHttpMethod = false

    protected UrlMappingsHolder urlMappingsHolder
    // Deliberately not UrlPathHelper.defaultInstance: that instance is read-only, and this field is
    // protected, so a subclass configuring it (alwaysUseFullPath and friends) must keep working.
    protected UrlPathHelper urlHelper = new UrlPathHelper()
    protected MimeTypeResolver mimeTypeResolver
    protected HandlerInterceptor[] webRequestHandlerInterceptors

    /**
     * The HTTP method to match URL mappings against.
     *
     * <p>An override the dispatcher already resolved is honoured wherever it applies, forwards and includes
     * included - the servlet filter's wrapper reports the overridden method for the whole of a request, and
     * an application is entitled to the same answer in either mode.</p>
     *
     * <p>Deriving a fresh override from the parameters is what an internal dispatch must not do: it inherits
     * the parameters of the request that started it, so a "_method" the dispatcher never acted on would go
     * on selecting an action for every forward after it.</p>
     */
    protected String resolveHttpMethod(HttpServletRequest request) {
        if (!resolveHiddenHttpMethod) {
            return request.getMethod()
        }
        String resolved = HiddenHttpMethod.effectiveMethod(request)
        if (resolved != request.getMethod() || WebUtils.isForwardOrInclude(request)) {
            return resolved
        }
        String override = HiddenHttpMethod.resolveOverride(request)
        if (override == null) {
            return resolved
        }
        // Published as well as returned. Routing on an override that allowedMethods cannot see would send
        // the request to the action the override names and then refuse it with a 405 for the method it
        // arrived as. The dispatcher normally publishes this before the mapping runs; this is the path
        // where it did not - a stock DispatcherServlet, or this mapping driven on its own.
        request.setAttribute(HiddenHttpMethod.OVERRIDDEN_METHOD_ATTRIBUTE, override)
        override
    }

    UrlMappingsHandlerMapping(UrlMappingsHolder urlMappingsHolder) {
        Assert.notNull(urlMappingsHolder, 'Argument [urlMappingsHolder] cannot be null')
        this.urlMappingsHolder = urlMappingsHolder
        setOrder(-5)
    }

    @Autowired
    void setHandlerInterceptors(HandlerInterceptor[] handlerInterceptors) {
        for (hi in handlerInterceptors) {
            if (!(hi instanceof MappedInterceptor)) {
                setInterceptors(hi)
            }
        }
    }

    @Autowired(required = false)
    void setWebRequestInterceptors(WebRequestInterceptor[] webRequestInterceptors) {
        webRequestHandlerInterceptors = webRequestInterceptors.collect({ WebRequestInterceptor wri ->
            new WebRequestHandlerInterceptorAdapter(wri)
        }) as HandlerInterceptor[]
    }

    @Autowired(required = false)
    void setMimeTypeResolver(MimeTypeResolver mimeTypeResolver) {
        this.mimeTypeResolver = mimeTypeResolver
    }

    @Override
    protected HandlerExecutionChain getHandlerExecutionChain(Object handler, HttpServletRequest request) {
        // Let Spring assemble the chain. Re-implementing it here meant Grails-mapped requests silently
        // missed whatever AbstractHandlerMapping added later - currently the API version deprecation
        // interceptor behind spring.mvc.apiversion.*.
        HandlerExecutionChain chain = super.getHandlerExecutionChain(handler, request)

        // WebRequestInterceptors have to come first, as these include things like Hibernate OSIV.
        if (webRequestHandlerInterceptors) {
            for (int i = 0; i < webRequestHandlerInterceptors.length; i++) {
                chain.addInterceptor(i, webRequestHandlerInterceptors[i])
            }
        }

        chain.addInterceptor(OBSERVATION_ROUTE_HANDLER)
        chain.addInterceptor(ERROR_HANDLING_HANDLER)
        return chain
    }

    @Override
    protected Object getHandlerInternal(HttpServletRequest request) throws Exception {

        def matchedInfo = request.getAttribute(MATCHED_REQUEST)
        def errorStatus = request.getAttribute(WebUtils.ERROR_STATUS_CODE_ATTRIBUTE)
        if (matchedInfo != null && errorStatus == null) return matchedInfo

        String uri = urlHelper.getPathWithinApplication(request)
        def webRequest = GrailsWebRequest.lookup(request)

        Assert.notNull(webRequest, 'HandlerMapping requires a Grails web request')

        String version = findRequestedVersion(webRequest)

        if (errorStatus && !WebUtils.isInclude(request)) {
            def exception = request.getAttribute(WebUtils.ERROR_EXCEPTION_ATTRIBUTE)
            UrlMappingInfo info
            if (exception instanceof Throwable) {
                exception = ExceptionUtils.getRootCause(exception)
                def exceptionSpecificMatch = urlMappingsHolder.matchStatusCode(errorStatus.toString().toInteger(), (Throwable) exception)
                if (exceptionSpecificMatch) {
                    info = exceptionSpecificMatch
                }
                else {
                    info = urlMappingsHolder.matchStatusCode(errorStatus.toString().toInteger())
                }
            }
            else {
                info = urlMappingsHolder.matchStatusCode(errorStatus.toString().toInteger())
            }

            request.setAttribute(MATCHED_REQUEST, info)
            return info
        }
        else {

            def infos = urlMappingsHolder.matchAll(uri, resolveHttpMethod(request), version != null ? version : UrlMapping.ANY_VERSION)

            for (UrlMappingInfo info in infos) {
                if (info) {
                    if (info.redirectInfo) return info

                    webRequest.resetParams()
                    info.configure(webRequest)
                    if (info instanceof GrailsControllerUrlMappingInfo) {
                        request.setAttribute(MATCHED_REQUEST, info)
                        request.setAttribute(GrailsApplicationAttributes.GRAILS_CONTROLLER_CLASS, ((GrailsControllerUrlMappingInfo) info).controllerClass)
                        return info
                    }
                    else if (info.viewName || info.URI) {
                        return info
                    }
                }
            }

            return null
        }

    }

    protected String findRequestedVersion(GrailsWebRequest currentRequest) {
        String version = currentRequest.getHeader(HttpHeaders.ACCEPT_VERSION)
        if (!version && mimeTypeResolver) {
            MimeType mimeType = mimeTypeResolver.resolveResponseMimeType(currentRequest)
            version = mimeType.version
        }
        return version
    }

    /**
     * Sets the HTTP server observation's route from the matched controller/action, so the
     * {@code http.server.requests} {@code uri} tag is a low-cardinality route (e.g. {@code /book/show})
     * instead of {@code UNKNOWN}. Runs at {@code preHandle}: at URL-mapping match the observation
     * context is not yet on the request.
     */
    @CompileStatic
    static class ObservationRouteHandler implements HandlerInterceptor {

        @Override
        boolean preHandle(HttpServletRequest request, HttpServletResponse response, Object handler) throws Exception {
            try {
                String controllerName = null
                String actionName = null
                def webRequest = GrailsWebRequest.lookup(request)
                if (webRequest != null) {
                    controllerName = webRequest.controllerName
                    actionName = webRequest.actionName
                }
                if ((controllerName == null || controllerName.isEmpty()) && handler instanceof UrlMappingInfo) {
                    def info = (UrlMappingInfo) handler
                    controllerName = info.controllerName
                    if (actionName == null || actionName.isEmpty()) {
                        actionName = info.actionName
                    }
                }
                if (controllerName != null && !controllerName.isEmpty()) {
                    if ((actionName == null || actionName.isEmpty()) && handler instanceof GrailsControllerUrlMappingInfo) {
                        actionName = ((GrailsControllerUrlMappingInfo) handler).controllerClass?.defaultAction
                    }
                    if (actionName == null || actionName.isEmpty()) {
                        actionName = 'index'
                    }
                    def context = ServerHttpObservationFilter.findObservationContext(request).orElse(null)
                    if (context != null) {
                        context.setPathPattern('/' + controllerName + '/' + actionName)
                    }
                }
            }
            catch (Throwable ignored) {
            }
            return true
        }
    }

    static class ErrorHandlingHandler implements HandlerInterceptor {

        @Override
        boolean preHandle(HttpServletRequest request, HttpServletResponse response, Object handler) throws Exception {
            return true
        }

        @Override
        void postHandle(HttpServletRequest request, HttpServletResponse response, Object handler, ModelAndView modelAndView) throws Exception {
            // no-op
        }

        @Override
        void afterCompletion(HttpServletRequest request, HttpServletResponse response, Object handler, Exception ex) throws Exception {
            request.removeAttribute(MATCHED_REQUEST)
        }
    }

    void setGrailsCorsConfiguration(GrailsCorsConfiguration grailsCorsConfiguration) {
        this.corsConfigurations = grailsCorsConfiguration.corsConfigurations
    }
}
