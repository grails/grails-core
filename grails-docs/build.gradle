configurations {
    // Required to keep Gradle classes off the test compile classpath.
    gradleConf.extendsFrom compile
}

dependencies {
    gradleConf gradleApi()
    compile "commons-lang:commons-lang:$commonsLangVersion",
            "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            "org.apache.ant:ant:$antVersion",
            'org.grails:grails-gdoc-engine:1.0.1',
            "org.yaml:snakeyaml:$snakeyamlVersion",
            "org.codehaus.groovy:groovy-ant:$groovyVersion"

    compile 'org.asciidoctor:asciidoctorj:1.5.4'
    compile('org.xhtmlrenderer:core-renderer:R8') {
        exclude group: 'bouncycastle', module:'bcprov-jdk14'
    }

    runtime "org.slf4j:slf4j-api:$slf4jVersion"

    runtime('com.lowagie:itext:2.0.8')

    compile 'org.jsoup:jsoup:1.7.3'
    testCompile "junit:junit:$junitVersion"
    testCompile("org.spockframework:spock-core:${spockVersion}") {
        transitive = false
        exclude group:'junit', module: 'junit-dep'    
        exclude group:'org.codehaus.groovy', module: 'groovy-all'    
    }
}

sourceSets {
    main {
        compileClasspath = configurations.gradleConf
    }
}

task docFilesJar(type: Jar, description: 'Package up files used for generating documentation.') {
    version = null
    archiveName = "grails-doc-files.jar"
    from "src/main/template"
    doLast {
        copy {
            from docFilesJar.archivePath
            into "${buildDir}/classes/main"
        }
    }
}

jar.dependsOn docFilesJar

eclipse {
    classpath {
       plusConfigurations += [configurations.gradleConf]
    }
}
