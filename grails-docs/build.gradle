configurations {
    // Required to keep Gradle classes off the test compile classpath.
    gradleConf.extendsFrom compileClasspath
}

dependencies {
    gradleConf gradleApi()
    api "commons-lang:commons-lang:$commonsLangVersion",
            "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            "org.apache.ant:ant:$antVersion",
            'org.grails:grails-gdoc-engine:1.0.1',
            'org.yaml:snakeyaml:1.33',
            "org.codehaus.groovy:groovy-ant:$groovyVersion"

    api 'org.asciidoctor:asciidoctorj:2.5.7'
    api('org.xhtmlrenderer:core-renderer:8.0') {
        exclude group: 'bouncycastle', module:'bcprov-jdk14'
    }

    runtimeOnly "org.slf4j:slf4j-api:$slf4jVersion"

    runtimeOnly('com.lowagie:itext:4.2.2')

    api 'org.jsoup:jsoup:1.15.4'
    testImplementation("org.spockframework:spock-core:${spockVersion}") { transitive = false }
}

sourceSets {
    main {
        compileClasspath = configurations.gradleConf
    }
}

tasks.register("docFilesJar", Jar) {
    description = 'Package up files used for generating documentation.'
    archiveVersion = null
    archiveFileName = "grails-doc-files.jar"
    from "src/main/template"
}

tasks.named('jar', Jar) {
    from docFilesJar
}

jar.dependsOn docFilesJar

eclipse {
    classpath {
       plusConfigurations += [configurations.gradleConf]
    }
}
