configurations {
    // Required to keep Gradle classes off the test compile classpath.
    gradleConf.extendsFrom compileClasspath
}

dependencies {
    gradleConf gradleApi()
    api "org.apache.commons:commons-text:$commonsTextVersion",
            "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            "org.apache.ant:ant:$antVersion",
            "org.grails:grails-gdoc-engine:$gdocEngineVersion",
            "org.yaml:snakeyaml:$snakeyamlVersion",
            "org.apache.groovy:groovy-ant:$groovyVersion"

    api "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
    api("org.xhtmlrenderer:core-renderer:$xhtmlRendererVersion") {
        exclude group: 'bouncycastle', module:'bcprov-jdk14'
    }

    runtimeOnly "org.slf4j:slf4j-api:$slf4jVersion"

    implementation "com.lowagie:itext:$itextVersion"

    api "org.jsoup:jsoup:$jsoupVersion"
    testImplementation("org.spockframework:spock-core:$spockVersion") { transitive = false }
}

sourceSets {
    main {
        compileClasspath = configurations.gradleConf
    }
}

tasks.register("docFilesJar", Jar) {
    description = 'Package up files used for generating documentation.'
    archiveVersion = null
    archiveFileName = "grails-doc-files.jar"
    from "src/main/template"
}

tasks.named('jar', Jar) {
    from docFilesJar
}

jar.dependsOn docFilesJar

eclipse {
    classpath {
       plusConfigurations += [configurations.gradleConf]
    }
}
