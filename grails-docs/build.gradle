configurations {
    // Required to keep Gradle classes off the test compile classpath.
    gradleConf.extendsFrom compileClasspath
}

dependencies {
    gradleConf gradleApi()
    api "org.apache.commons:commons-text",
            "org.slf4j:jcl-over-slf4j",
            "org.apache.ant:ant",
            "org.grails:grails-gdoc-engine",
            "org.yaml:snakeyaml",
            "org.apache.groovy:groovy-ant"

    api "org.asciidoctor:asciidoctorj"
    implementation "org.xhtmlrenderer:flying-saucer-pdf-openpdf"

    runtimeOnly "org.slf4j:slf4j-api"

    api "org.jsoup:jsoup"
    testImplementation("org.spockframework:spock-core") { transitive = false }
}

sourceSets {
    main {
        compileClasspath = configurations.gradleConf
    }
}

tasks.register("docFilesJar", Jar) {
    description = 'Package up files used for generating documentation.'
    archiveVersion = null
    archiveFileName = "grails-doc-files.jar"
    from "src/main/template"
}

tasks.named('jar', Jar) {
    from docFilesJar
}

jar.dependsOn docFilesJar

eclipse {
    classpath {
       plusConfigurations += [configurations.gradleConf]
    }
}
