configurations {
    // Required to keep Gradle classes off the test compile classpath.
    gradleConf.extendsFrom compile
}

dependencies {
    gradleConf gradleApi()
    compile "commons-lang:commons-lang:$commonsLangVersion",
            "org.slf4j:jcl-over-slf4j:$slf4jVersion",
            "org.apache.ant:ant:$antVersion",
            'org.xhtmlrenderer:core-renderer:R8',
            'org.grails:grails-gdoc-engine:1.0.1',
            'org.yaml:snakeyaml:1.8'

    testCompile "junit:junit:$junitVersion"
    testCompile('org.spockframework:spock-core:0.6-groovy-1.8-SNAPSHOT') {
        transitive = false
    }
}

sourceSets {
    main {
        compileClasspath = configurations.gradleConf
    }
}

task docFilesJar(type: Jar, description: 'Package up files used for generating documentation.') {
    version = null
    appendix = "doc-files"
    from "src/main/template"
    doLast {
        copy {
            from docFilesJar.archivePath
            into "${buildDir}/classes/main"
        }
    }
}

jar.dependsOn docFilesJar
jar.appendix = 'docs'

eclipse {
    classpath {
       plusConfigurations += configurations.gradleConf
    }
}
