/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.web.i18n

import grails.util.GrailsWebMockUtil
import org.junit.jupiter.api.AfterEach
import org.junit.jupiter.api.Test
import org.springframework.mock.web.MockHttpServletRequest
import org.springframework.mock.web.MockHttpServletResponse
import org.springframework.mock.web.MockServletContext
import org.springframework.web.context.request.RequestContextHolder
import org.springframework.web.servlet.DispatcherServlet
import org.springframework.web.servlet.i18n.AcceptHeaderLocaleResolver
import org.springframework.web.servlet.i18n.SessionLocaleResolver

import static org.junit.jupiter.api.Assertions.assertEquals
import static org.junit.jupiter.api.Assertions.assertNotEquals
import static org.junit.jupiter.api.Assertions.assertThrows

/**
 * @author Graeme Rocher
 * @since 1.0
 */
class ParamsAwareLocaleChangeInterceptorTests {

    @AfterEach
    protected void tearDown() {
        RequestContextHolder.resetRequestAttributes()
    }

    @Test
    void testSwitchLocaleWithStringArrayParamsObject() {

        def webRequest = GrailsWebMockUtil.bindMockWebRequest()

        def request = webRequest.getCurrentRequest()
        def response = webRequest.getCurrentResponse()

        SessionLocaleResolver localeResolver = new SessionLocaleResolver()

        request.setAttribute(DispatcherServlet.LOCALE_RESOLVER_ATTRIBUTE,localeResolver)

        def localeChangeInterceptor = new ParamsAwareLocaleChangeInterceptor()
        localeChangeInterceptor.paramName = "lang"

        def locale = localeResolver.resolveLocale(request)
        assert localeChangeInterceptor.preHandle(request, response, null)

        assertEquals locale, localeResolver.resolveLocale(request)

        webRequest.params.lang = ["de_DE", "en_GB"] as String[]

        assert localeChangeInterceptor.preHandle(request, response, null)

        assertNotEquals locale, localeResolver.resolveLocale(request)

        locale = localeResolver.resolveLocale(request)

        assertEquals "de", locale.getLanguage()
        assertEquals "DE", locale.getCountry()
    }

    @Test
    void testSwitchLocaleWithParamsObject() {

        def webRequest = GrailsWebMockUtil.bindMockWebRequest()

        def request = webRequest.getCurrentRequest()
        def response = webRequest.getCurrentResponse()

        SessionLocaleResolver localeResolver = new SessionLocaleResolver()

        request.setAttribute(DispatcherServlet.LOCALE_RESOLVER_ATTRIBUTE,localeResolver)

        def localeChangeInterceptor = new ParamsAwareLocaleChangeInterceptor()
        localeChangeInterceptor.paramName = "lang"

        def locale = localeResolver.resolveLocale(request)
        assert localeChangeInterceptor.preHandle(request, response, null)

        assertEquals locale, localeResolver.resolveLocale(request)

        webRequest.params.lang = "de_DE"

        assert localeChangeInterceptor.preHandle(request, response, null)

        assertNotEquals locale, localeResolver.resolveLocale(request)

        locale = localeResolver.resolveLocale(request)

        assertEquals "de", locale.getLanguage()
        assertEquals "DE", locale.getCountry()
    }

    @Test
    void testReadOnlyLocaleResolverIsIgnoredGracefully() {

        def webRequest = GrailsWebMockUtil.bindMockWebRequest()

        def request = webRequest.getCurrentRequest()
        def response = webRequest.getCurrentResponse()

        // AcceptHeaderLocaleResolver.setLocale throws UnsupportedOperationException
        def localeResolver = new AcceptHeaderLocaleResolver()

        request.setAttribute(DispatcherServlet.LOCALE_RESOLVER_ATTRIBUTE, localeResolver)

        def localeChangeInterceptor = new ParamsAwareLocaleChangeInterceptor()
        localeChangeInterceptor.paramName = "lang"

        webRequest.params.lang = "de_DE"

        // preHandle must not propagate the UnsupportedOperationException from the read-only resolver
        assert localeChangeInterceptor.preHandle(request, response, null)
    }

    @Test
    void testSwitchLocaleWithRequestParameter() {

        def webRequest = GrailsWebMockUtil.bindMockWebRequest()

        MockHttpServletRequest request = webRequest.getCurrentRequest()
        def response = webRequest.getCurrentResponse()

        SessionLocaleResolver localeResolver = new SessionLocaleResolver()

        request.setAttribute(DispatcherServlet.LOCALE_RESOLVER_ATTRIBUTE,localeResolver)

        def localeChangeInterceptor = new ParamsAwareLocaleChangeInterceptor()
        localeChangeInterceptor.paramName = "lang"

        def locale = localeResolver.resolveLocale(request)
        assert localeChangeInterceptor.preHandle(request, response, null)

        assertEquals locale, localeResolver.resolveLocale(request)

        request.addParameter "lang", "de_DE"

        assert localeChangeInterceptor.preHandle(request, response, null)

        assertNotEquals locale, localeResolver.resolveLocale(request)

        locale = localeResolver.resolveLocale(request)

        assertEquals "de", locale.getLanguage()
        assertEquals "DE", locale.getCountry()
    }

    @Test
    void testMultipartRequestWithUnreadableParametersIsNotIntercepted() {
        // This interceptor runs on the error dispatch too, so it meets the oversized upload whose parts
        // the container refused to parse. Throwing here would replace the error being rendered.
        def request = unreadableParameterRequest('multipart/form-data; boundary=test')
        def webRequest = GrailsWebMockUtil.bindMockWebRequest(new MockServletContext(), request,
                new MockHttpServletResponse())

        def localeChangeInterceptor = new ParamsAwareLocaleChangeInterceptor()
        localeChangeInterceptor.paramName = "lang"

        assert localeChangeInterceptor.preHandle(request, webRequest.getCurrentResponse(), null)
    }

    @Test
    void testUnreadableParametersStillThrowForANonMultipartRequest() {
        def request = unreadableParameterRequest('application/x-www-form-urlencoded')
        def webRequest = GrailsWebMockUtil.bindMockWebRequest(new MockServletContext(), request,
                new MockHttpServletResponse())

        def localeChangeInterceptor = new ParamsAwareLocaleChangeInterceptor()
        localeChangeInterceptor.paramName = "lang"

        def e = assertThrows(IllegalStateException) {
            localeChangeInterceptor.preHandle(request, webRequest.getCurrentResponse(), null)
        }

        assertEquals 'parameters are unreadable', e.message
    }

    private static MockHttpServletRequest unreadableParameterRequest(String contentType) {
        def request = new MockHttpServletRequest() {

            @Override
            Map<String, String[]> getParameterMap() {
                throw new IllegalStateException('parameters are unreadable')
            }

            @Override
            String getParameter(String name) {
                throw new IllegalStateException('parameters are unreadable')
            }
        }
        request.contentType = contentType
        request.method = 'POST'
        request
    }
}
