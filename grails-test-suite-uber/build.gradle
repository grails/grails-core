configurations.testCompile {
    exclude module: "grails-plugin-testing"
}

dependencies {

    compile project(':grails-test-suite-base'),
            project(':grails-plugin-interceptors'),        
            project(':grails-plugin-controllers')

    compile "org.apache.tomcat:tomcat-jdbc:$tomcatVersion"

    testRuntime 'javax.servlet.jsp:jsp-api:2.2.1'
    testRuntime 'jstl:jstl:1.2'
    testRuntime 'javax.el:el-api:2.2'
    testRuntime "com.h2database:h2:$h2Version"
    testRuntime "org.springframework:spring-aspects:${springVersion}"
    testRuntime "org.aspectj:aspectjrt:$aspectjVersion",
            "org.aspectj:aspectjweaver:$aspectjVersion"

    testCompile project(':grails-plugin-codecs'),
                project(':grails-plugin-domain-class') ,
                project(':grails-plugin-url-mappings') ,
                project(":grails-plugin-datasource"),
                project(":grails-plugin-services"),
                project(":grails-plugin-rest"),
                project(":grails-plugin-i18n"),
                project(":grails-plugin-databinding"),
                project(':grails-spring')

    testCompile "org.grails:grails-datastore-gorm-hibernate5:$hibernateDatastoreVersion", {
        exclude group: 'org.grails', module:'grails-datastore-gorm'
        exclude group: 'org.grails', module:'grails-datastore-core'
        exclude group: 'org.springframework', module:'spring-core'
        exclude group: 'org.springframework', module:'spring-context'
        exclude group: 'org.springframework', module:'spring-web'
        exclude group: 'org.springframework', module:'spring-beans'
        exclude group: 'org.springframework', module:'spring-tx'
        exclude group: 'org.slf4j', module:'jcl-over-slf4j'
        exclude group: 'org.slf4j', module:'jul-to-slf4j'
        exclude group: 'org.slf4j', module:'slf4j-api'
        exclude group: 'org.slf4j', module:'slf4j-simple'
        exclude group: 'org.grails', module:'grails-bootstrap'
        exclude group: 'org.grails', module:'grails-plugin-domain-class'
        exclude group: 'org.grails', module:'grails-core'
        exclude group: 'org.grails', module:'grails-web'
        exclude group: 'org.grails', module:'grails-test'
        exclude group: 'org.grails', module:'grails-async'
        exclude group: 'org.grails', module:'grails-spring'
        exclude group: 'commons-collections', module:'commons-collections'
        exclude group: 'commons-lang', module:'commons-lang'
        exclude group: 'javassit', module:'javassist'
        exclude group: 'java-persistence', module:'persistence-api'
        exclude group: 'javassist', module: 'javassist'
    }

    testCompile "org.grails.plugins:async:$asyncVersion", {
        exclude module:'grails-plugin-codecs'
        exclude module:'grails-plugin-controllers'
        exclude module:'grails-core'
        exclude module:'grails-plugin-domain-class'
    }
    testCompile "org.grails.plugins:gsp:$gspVersion", {
        exclude module:'grails-plugin-codecs'
        exclude module:'grails-plugin-controllers'
        exclude module:'grails-core'
        exclude module:'grails-plugin-domain-class'
        exclude module:'grails-web-common'
        exclude module:'grails-encoder'
    }
    testCompile "org.grails:grails-gorm-testing-support:$testingSupportVersion", {
        exclude module:'grails-plugin-codecs'
        exclude module:'grails-plugin-domain-class'
        exclude module:'grails-test'
        exclude module:'grails-core'
        exclude module:'async'
    }
    testCompile "org.grails:grails-web-testing-support:$testingSupportVersion", {
        exclude module:'grails-plugin-codecs'
        exclude module:'grails-plugin-rest'
        exclude module:'grails-plugin-interceptors'
        exclude module:'grails-test'
        exclude module:'grails-core'
        exclude module:'async'
        exclude module:'gsp'
    }
}

test {
     maxParallelForks = isTravisBuild ? 2 : 4
     forkEvery = isTravisBuild ? 25 : 100
     if(!isTravisBuild) {
         maxHeapSize = '1024m'
     }
     excludes = [
         "**/grails/test/PersonTests.class",
         "**/rest/render/**/*Spec.class",
         "**/*TestCase.class",
         "**/DataSourceGrailsPluginTests",
         "**/DefaultGrailsControllerClassTests.class",
         "**/GrailsUnitTestCaseTests.class",
         "**/SetupTeardownInvokeTests.class",
         "**/TestMixinSetupTeardownInvokeTests.class",
         "**/UrlMappingsTestMixinTests.class",
         "**/WebUtilsTests.class",
         "**/RestfulControllerSpec.class",
         "**/ResourceAnnotationRestfulControllerSpec.class",
         "**/TestingValidationSpec.class",
         "**/CascadingErrorCountSpec"
     ]
}

task isolatedTestsOne(type:Test) {
    includes = [
        "**/DataSourceGrailsPluginTests.class",
        "**/GrailsUnitTestCaseTests.class",
        "**/WebUtilsTests.class"
    ]
}

task isolatedTestsTwo(type:Test) {
    maxParallelForks = 1
    forkEvery = 100
    includes = [
        "**/UrlMappingsTestMixinTests.class",
        "**/SetupTeardownInvokeTests.class",
        "**/TestMixinSetupTeardownInvokeTests.class"
    ]
}

task isolatedRestRendererTests(type:Test) {
    includes = ['**/rest/render/**/*Spec.class']
}

task isolatedDefaultGrailsControllerClassTests(type: Test) {
    includes = ['**/DefaultGrailsControllerClassTests.class']
}

task isolatedPersonTests(type: Test) {
    includes = ['**/grails/test/PersonTests.class',
                "**/TestingValidationSpec.class",
                "**/CascadingErrorCountSpec"
                ]
}

task isolatedRestfulControllerTests(type:Test) {
    includes = ["**/RestfulControllerSpec.class",
        "**/ResourceAnnotationRestfulControllerSpec.class"]
}

test.dependsOn isolatedPersonTests, isolatedTestsOne, isolatedTestsTwo, isolatedRestRendererTests, isolatedDefaultGrailsControllerClassTests, isolatedRestfulControllerTests
