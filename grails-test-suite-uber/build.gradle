dependencies {

    compile project(':grails-test-suite-base'),
            project(':grails-plugin-testing')

    testRuntime 'javax.servlet.jsp:jsp-api:2.1'
    testRuntime 'jstl:jstl:1.1.2'
    testRuntime 'javax.el:el-api:1.0'

    testCompile "commons-io:commons-io:${commonsIOVersion}"
    testCompile project(':grails-plugin-codecs'),
                project(':grails-plugin-domain-class') ,
                project(':grails-plugin-url-mappings') ,
                project(':grails-plugin-validation'),
                project(":grails-plugin-datasource"),
                project(":grails-plugin-servlets"),
                project(":grails-plugin-services"),
                project(":grails-plugin-rest"),
                project(":grails-plugin-i18n"),
                project(":grails-plugin-log4j"),
                project(":grails-plugin-databinding")

    testCompile "org.grails:grails-datastore-gorm-hibernate:$datastoreVersion", {
        exclude group: 'org.grails', module:'grails-datastore-gorm'
        exclude group: 'org.grails', module:'grails-datastore-core'
        exclude group: 'org.springframework', module:'spring-core'
        exclude group: 'org.springframework', module:'spring-context'
        exclude group: 'org.springframework', module:'spring-web'
        exclude group: 'org.springframework', module:'spring-beans'
        exclude group: 'org.springframework', module:'spring-tx'
        exclude group: 'org.slf4j', module:'jcl-over-slf4j'
        exclude group: 'org.slf4j', module:'jul-to-slf4j'
        exclude group: 'org.slf4j', module:'slf4j-api'
        exclude group: 'org.slf4j', module:'slf4j-simple'
        exclude group: 'org.grails', module:'grails-bootstrap'
        exclude group: 'org.grails', module:'grails-plugin-domain-class'
        exclude group: 'org.grails', module:'grails-core'
        exclude group: 'org.grails', module:'grails-web'
        exclude group: 'org.grails', module:'grails-test'
        exclude group: 'org.grails', module:'grails-async'
        exclude group: 'commons-collections', module:'commons-collections'
        exclude group: 'commons-lang', module:'commons-lang'
        exclude group: 'javassit', module:'javassist'
        exclude group: 'java-persistence', module:'persistence-api'
        exclude group: 'javassist', module: 'javassist'
    }
}

test {
     maxParallelForks = isContinuousIntegrationBuild ? 2 : 4
     forkEvery = isContinuousIntegrationBuild ? 25 : 100
     if(!isContinuousIntegrationBuild) {
         maxHeapSize = '1024m'
     }
     excludes = [
         "**/grails/test/PersonTests.class",
         "**/rest/render/**/*Spec.class",
         "**/*TestCase.class",
         "**/DataSourceGrailsPluginTests",
         "**/DefaultGrailsControllerClassTests.class",
         "**/MockUtilsTests.class",
         "**/GrailsUnitTestCaseTests.class",
         "**/SetupTeardownInvokeTests.class",
         "**/TestMixinSetupTeardownInvokeTests.class",
         "**/UrlMappingsTestMixinTests.class",
         "**/WebUtilsTests.class",
         "**/RestfulControllerSpec.class",
         "**/ResourceAnnotationRestfulControllerSpec.class",
         "**/SimpleGrailsControllerTests.class"
     ]
}

task isolatedTestsOne(type:Test) {
    includes = [
        "**/DataSourceGrailsPluginTests.class",
        "**/MockUtilsTests.class",
        "**/GrailsUnitTestCaseTests.class",
        "**/SetupTeardownInvokeTests.class",
        "**/TestMixinSetupTeardownInvokeTests.class",
        "**/UrlMappingsTestMixinTests.class",
        "**/WebUtilsTests.class"
    ]
}

task isolatedTestsTwo(type:Test) {
    maxParallelForks = 1
    forkEvery = 1
    includes = [
        "**/UrlMappingsTestMixinTests.class",
        "**/SetupTeardownInvokeTests.class",
        "**/TestMixinSetupTeardownInvokeTests.class",
        "**/SimpleGrailsControllerTests.class"
    ]
}

task isolatedRestRendererTests(type:Test) {
    includes = ['**/rest/render/**/*Spec.class']
}

task isolatedDefaultGrailsControllerClassTests(type: Test) {
    includes = ['**/DefaultGrailsControllerClassTests.class']
}

task isolatedPersonTests(type: Test) {
    includes = ['**/grails/test/PersonTests.class']
}

task isolatedRestfulControllerTests(type:Test) {
    includes = ["**/RestfulControllerSpec.class",
        "**/ResourceAnnotationRestfulControllerSpec.class"]
}

test.dependsOn isolatedPersonTests, isolatedTestsOne, isolatedTestsTwo, isolatedRestRendererTests, isolatedDefaultGrailsControllerClassTests, isolatedRestfulControllerTests

