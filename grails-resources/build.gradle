task createSharedFilesJar(type: Jar, description: 'Package up the files that are shared by both applications and plugins.') {
    version = null
    baseName = "grails"
    appendix = "shared-files"
    ext.appFileJar = true
    from("src/war/WEB-INF") {
        into "web-app/WEB-INF"
        include "applicationContext.xml", "sitemesh.xml", "tld/*.tld"
    }
    from("src/grails/grails-app/conf") {
        into "grails-app/conf"
        include "DataSource.groovy", "UrlMappings.groovy"
    }
}

task createSharedApplicationFilesJar(type: Jar, description: 'Package up the files that are exclusive to applications.') {
    version = null
    baseName = "grails"
    appendix = "app-files"
    ext.appFileJar = true
    from("src/war") {
        into "web-app"
        exclude "WEB-INF/**"
    }
    from("src/grails/grails-app") {
        into "grails-app"
        exclude "taglib/**", "utils/**", "conf/DataSource.groovy", "conf/UrlMappings.groovy"
    }
}

task createSharedPluginFilesJar(type: Jar, description: 'Package up the files that are exclusive to plugins.') {
    version = null
    baseName = "grails"
    appendix = "plugin-files"
    ext.appFileJar = true
    from("src/grails/templates/plugins")
    from("src/grails") {
        include "grails-app/views/error.gsp"
    }
    from("src/grails/plugin")
}

task createIntegrationFilesJar(type: Jar, description: 'Files to integrate Grails with IDEs and build systems.') {
    version = null
    baseName = "grails"
    appendix = "integration-files"
    ext.appFileJar = true
    from("src/grails/templates/ide-support", "src/grails/ant")
}

ext.allJarFileTasks = tasks.matching { it.hasProperty("appFileJar") }
task resourceJarFiles(dependsOn: allJarFileTasks)

jar {
    from allJarFileTasks
    from("src/war") {
        into "src/war"
        include "WEB-INF/**"
        exclude 'WEB-INF/tld/grails.tld', 'WEB-INF/tld/spring.tld'
    }
    from("src/grails/templates") {
        into "src/grails/templates"
    }
}
