configurations {
    jsp21.extendsFrom compile
}

dependencies {
    jsp21 'javax.servlet.jsp:jsp-api:2.1'

    compile project(":grails-bootstrap"), project(":grails-core"), project(":grails-spring")
    compile('commons-el:commons-el:1.0') {
        exclude module:'commons-logging'
    }

    compile ("commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile "commons-collections:commons-collections:${commonsCollectionsVersion}"
    compile('commons-fileupload:commons-fileupload:1.2.2')
    compile "commons-io:commons-io:${commonsIOVersion}"
    compile "commons-lang:commons-lang:$commonsLangVersion"

    compile 'opensymphony:sitemesh:2.4'

    compile "org.springframework:spring-webmvc:${springVersion}"

    // Required for JSP support
    compile 'javax.servlet:jsp-api:2.0'
    compile 'xpp3:xpp3_min:1.1.4c'
    runtime 'taglibs:standard:1.1.2'
    runtime "javax.servlet:jstl:1.1.2"
}

sourceSets {
    jsp21 {
        compileClasspath = files(configurations.jsp21, main.output)
        groovy.srcDirs = ['src/jsp21/groovy']
        resources.srcDirs = []
    }
}

jar {
    from compileJsp21Groovy.outputs.files
}

jar.appendix = 'web'

eclipse {
    classpath {
       plusConfigurations += configurations.jsp21
    }
}
