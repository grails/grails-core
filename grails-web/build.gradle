configurations {
    jsp21.extendsFrom compile
    optional
}

dependencies {
    jsp21 'javax.servlet.jsp:jsp-api:2.1'

    compile project(":grails-core"), project(":grails-spring"), project(":grails-async"), project(":grails-databinding")
    compile('commons-el:commons-el:1.0') {
        exclude module:'commons-logging'
    }

    compile ("commons-beanutils:commons-beanutils:${commonsBeanUtilsVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile "commons-collections:commons-collections:${commonsCollectionsVersion}"
    compile('commons-fileupload:commons-fileupload:1.2.2')
    compile "commons-io:commons-io:${commonsIOVersion}"
    compile "commons-lang:commons-lang:$commonsLangVersion"

    compile( "com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:${concurrentlinkedhashmapVersion}" ) {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile 'opensymphony:sitemesh:2.4'
    compile "junit:junit:${junitVersion}"

    compile "org.springframework:spring-webmvc:${springVersion}"

    // Required for JSP support
    compile 'javax.servlet:jsp-api:2.0', {
        ext.notInPom = true
        exclude group: 'javax.servlet', module:'servlet-api'
    }
    compile 'xpp3:xpp3_min:1.1.4c'

    compile 'com.google.code.gson:gson:2.2.4'

    runtime 'taglibs:standard:1.1.2'
    runtime "javax.servlet:jstl:1.1.2"

    optional "org.apache.ant:ant:${antVersion}"
    optional "jline:jline:$jlineVersion"
    optional "org.fusesource.jansi:jansi:$jansiVersion"

    optional("org.codehaus.gant:gant_groovy1.8:${gantVersion}") {
        exclude group:'commons-cli', module:'commons-cli'
    }

    compile 'org.objenesis:objenesis:1.3'
}

sourceSets {
    main {
        compileClasspath += files(configurations.optional)
    }
    jsp21 {
        compileClasspath = files(configurations.jsp21, main.output)
        groovy.srcDirs = ['src/jsp21/groovy']
        resources.srcDirs = []
    }
}

javadoc.classpath += files(configurations.optional)
jar {
    from compileJsp21Groovy.outputs.files
}

eclipse {
    classpath {
        plusConfigurations += configurations.jsp21
        file {
            whenMerged { classpath ->
                // move jsp-api-2.1 to the last one in entries so that it appears after -2.0
                def jsp21 = classpath.entries.find { entry -> entry?.path =~ /jsp-api-2.1.jar$/ }
                if (jsp21) {
                    classpath.entries.remove(jsp21)
                    classpath.entries << jsp21
                }
            }
        }
    }
}
