// src: https://github.com/brunodecarvalho/gradle-plugins/blob/master/integration-test.gradle
// Add integration test source sets
sourceSets {
  integrationTest { sourceSet ->
    ["java", "groovy", "scala", "resources"].each {
      if (!sourceSet.hasProperty(it)) return
      sourceSet."$it".srcDir file("src/integration-test/${it}")
    }
  }
}

configurations {
  integrationTestImplementation.extendsFrom configurations.testCompileClasspath
  integrationTestRuntimeOnly.extendsFrom configurations.testRuntimeClasspath
}

// Setup dependencies for integration testing
dependencies {
  integrationTestImplementation sourceSets.main.output
  integrationTestImplementation sourceSets.test.output
}

// Define integration test task
task integrationTest(type: Test) {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  reports.html.getRequired().set(false)
}

// Make sure 'check' task calls integration test
check.dependsOn integrationTest

// Merge test reports
task mergeTestReports(type: TestReport) {
  destinationDirectory = file("$buildDir/reports/tests")

  // These must point to the binary test results directory generated by a Test task instance.
  // If Test task instances are specified directly, this task would depend on them and run them.
  reportOn files("$buildDir/test-results/binary/test", "$buildDir/test-results/binary/integrationTest")
}
integrationTest.finalizedBy mergeTestReports

// IDE integration for IDEA. Eclipse plugin already handles all source folders.
idea {
  module {
    ["java", "groovy", "scala", "resources"].each {
      def srcDir = file("src/integration-test/$it")
      if(srcDir.exists()) {
        testSourceDirs += srcDir
      }
    }
  }
}

