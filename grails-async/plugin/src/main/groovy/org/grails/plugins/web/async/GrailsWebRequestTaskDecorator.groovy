/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.plugins.web.async

import groovy.transform.CompileStatic

import org.springframework.core.task.TaskDecorator
import org.springframework.web.context.request.RequestAttributes
import org.springframework.web.context.request.RequestContextHolder

import org.grails.web.servlet.mvc.GrailsWebRequest
import org.grails.web.util.WebUtils

/**
 * Propagates the current Grails web request through a Spring-managed task executor.
 *
 * @since 8.0
 */
@CompileStatic
class GrailsWebRequestTaskDecorator implements TaskDecorator {

    @Override
    Runnable decorate(Runnable task) {
        GrailsWebRequest captured = GrailsWebRequest.lookup()
        if (captured == null) {
            return task
        }

        return {
            RequestAttributes previous = RequestContextHolder.getRequestAttributes()
            GrailsWebRequest taskRequest = new GrailsWebRequest(
                    captured.currentRequest,
                    captured.currentResponse,
                    captured.attributes)
            WebUtils.storeGrailsWebRequest(taskRequest)
            try {
                task.run()
            }
            finally {
                if (previous == null) {
                    RequestContextHolder.resetRequestAttributes()
                }
                else {
                    RequestContextHolder.setRequestAttributes(previous)
                }
            }
        }
    }
}
