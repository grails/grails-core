dependencies {
    compileOnly "org.aspectj:aspectjrt:$aspectjVersion",
            "org.aspectj:aspectjweaver:$aspectjVersion"
    api "jakarta.inject:jakarta.inject-api:$jakartaInjectVersion"
    api "jakarta.persistence:jakarta.persistence-api:$jakartaPersistenceVersion"
    api "jakarta.annotation:jakarta.annotation-api:$jakartaAnnotationApiVersion"

    implementation "com.github.ben-manes.caffeine:caffeine:$caffeineVersion"
    api("org.springframework.boot:spring-boot:$springBootVersion")
    api("org.springframework:spring-core:$springVersion") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    api("org.springframework:spring-tx:$springVersion") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    api("org.springframework:spring-beans:$springVersion") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    api("org.springframework:spring-context:$springVersion") {
        exclude group: 'commons-logging', module:'commons-logging'
    }

    compileOnly("org.springframework:spring-test:$springVersion") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compileOnly("org.apache.groovy:groovy-templates:$groovyVersion")


    api project(":grails-bootstrap"), {
        exclude group:"org.fusesource.jansi", module:"jansi"
        exclude group:"jline", module:"jline"
        exclude group:"net.java.dev.jna", module:"jna"

        // Ant
        exclude group:"org.apache.groovy", module:"groovy-ant"
        exclude group:"org.apache.ant", module:"ant"
        exclude group:"org.apache.ant", module:"ant-launcher"
        exclude group:"org.apache.ivy", module:"ivy"
        exclude group:"org.apache.ant", module:"ant-trax"
        exclude group:"org.apache.ant", module:"ant-junit"

        exclude group:"org.codehaus.gant", module:"gant_groovy1.8"
    }

    api project(":grails-spring"), {
        exclude group:'org.springframework', module:'spring-tx'
        exclude group:'org.springframework', module:'spring-web'
        exclude group:'org.grails', module:'grails-bootstrap'
    }

    api "org.grails:grails-datastore-core:$datastoreVersion"

    testImplementation("org.springframework:spring-jdbc:$springVersion") {
        exclude group: 'commons-logging', module:'commons-logging'
    }

    testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"

    testRuntimeOnly "com.h2database:h2:$h2Version"
    testRuntimeOnly "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    // These dependencies are not required, but due to a Groovy compiler bug they are loaded by Groovy and hence
    // have to be on the path for compilation even though they shouldn't be
    api "org.apache.ant:ant:$antVersion"
    api "jline:jline:$jlineVersion"
    api "org.fusesource.jansi:jansi:$jansiVersion"

}

def writeProps = tasks.register('writeGrailsProperties', WriteProperties) {
    outputFile = file("${buildDir}/grails.build.properties")
    property("grails.version", project.version)
}

tasks.named('processResources') {
    from writeProps
}

eclipse {
    classpath {
       file {
           whenMerged { classpath ->
                // don't add global ASTs since the source code is in this project and that causes greclipse compilation to fail
                classpath.entries.find { entry -> entry.kind == 'src' && entry.path == "src/main/resources" }?.excludes=["META-INF/services/org.codehaus.groovy.transform.ASTTransformation"]
           }
       }
    }
}
