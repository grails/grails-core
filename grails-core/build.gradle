configurations {
    optional
}
dependencies {

    compile 'javax.servlet:javax.servlet-api:3.0.1', provided
    compile 'org.hibernate.javax.persistence:hibernate-jpa-2.0-api:1.0.1.Final'
    compile "commons-collections:commons-collections:${commonsCollectionsVersion}"
    compile "commons-io:commons-io:${commonsIOVersion}"
    compile "commons-lang:commons-lang:$commonsLangVersion"
    compile( "commons-validator:commons-validator:1.3.1" ) {
        exclude group: 'xml-apis', module:'xml-apis'
        exclude group: 'commons-digester', module:'commons-digester'
        exclude group: 'commons-logging', module:'commons-logging'
        exclude group: 'commons-beanutils', module:'commons-beanutils'
    }
    compile( "com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:${concurrentlinkedhashmapVersion}" ) {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile("org.springframework:spring-aop:${springVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile("org.springframework:spring-aspects:${springVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile("org.springframework:spring-core:${springVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile("org.springframework:spring-beans:${springVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile("org.springframework:spring-context:${springVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile("org.springframework:spring-context-support:${springVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
    }
    compile("org.springframework:spring-expression:${springVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
    }

    compile("org.springframework:spring-test:${springVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
        ext.notInPom = true
    }

    compile("org.springframework:spring-web:${springVersion}") {
        exclude group: 'commons-logging', module:'commons-logging'
    }

    runtime 'oro:oro:2.0.8'
    runtime 'cglib:cglib:2.2.2' // Used to generate proxies for classes via Spring AOP
    runtime 'asm:asm:3.3.1'
    runtime 'aopalliance:aopalliance:1.0',
            "org.aspectj:aspectjrt:$aspectjVersion",
            "org.aspectj:aspectjweaver:$aspectjVersion"

    runtime "org.springframework:spring-jms:${springVersion}", {
        ext.notInPom = true
    }
    runtime ( 'xalan:serializer:2.7.1' ) {
        exclude group: 'xml-apis', module:'xml-apis'
    }

    compile ("org.springsource.springloaded:springloaded-core:$springLoadedVersion") {
        ext.notInPom = true
    }

    compile "org.springframework:spring-tx:${springVersion}"
    compile project(":grails-bootstrap"), {
        exclude group:"org.fusesource.jansi", module:"jansi"
        exclude group:"jline", module:"jline"
        exclude group:"net.java.dev.jna", module:"jna"

        // Ant
        exclude group:"org.apache.ant", module:"ant"
        exclude group:"org.apache.ant", module:"ant-launcher"
        exclude group:"org.apache.ivy", module:"ivy"
        exclude group:"org.apache.ant", module:"ant-trax"
        exclude group:"org.apache.ant", module:"ant-junit"

        exclude group:"org.codehaus.gant", module:"gant_groovy1.8"
    }

    optional "org.apache.ant:ant:${antVersion}"
    optional "jline:jline:$jlineVersion"
    optional "org.fusesource.jansi:jansi:$jansiVersion"

    optional("org.codehaus.gant:gant_groovy1.8:${gantVersion}") {
        exclude group:'commons-cli', module:'commons-cli'
    }
    compile project(":grails-spring")
}

sourceSets {
    main {
        compileClasspath += files(configurations.optional)
    }
    test {
        compileClasspath += files(configurations.optional)
        runtimeClasspath += files(configurations.optional)
    }
}
javadoc.classpath += files(configurations.optional)
compileGroovy.doFirst {
    delete("${buildDir}/classes/main/META-INF")
    ant.mkdir(dir:sourceSets.main.output.classesDir)
    ant.propertyfile(file:"${sourceSets.main.output.classesDir}/grails.build.properties") {
         entry key:"grails.version", value:version
    }
}
