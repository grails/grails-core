/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.plugins.sitemesh3

import groovy.transform.CompileStatic

import org.sitemesh.webmvc.SiteMeshViewResolverBeanPostProcessor

import org.springframework.beans.BeansException
import org.springframework.beans.factory.BeanFactory
import org.springframework.util.ObjectUtils

/**
 * {@link SiteMeshViewResolverBeanPostProcessor} preconfigured to wrap
 * Grails' {@code gspViewResolver} bean with a
 * {@link GrailsSiteMeshViewResolver}.
 */
@CompileStatic
class GrailsSiteMeshViewResolverBeanPostProcessor extends SiteMeshViewResolverBeanPostProcessor {

    /**
     * The primary GSP view resolver bean name in Grails. The historical
     * name {@code jspViewResolver} is kept for compatibility with the
     * plugin's {@code GroovyPagesPostProcessor}, which registers the GSP
     * resolver under that name when it isn't already present (see
     * {@code org.grails.plugins.web.GroovyPagesPostProcessor}). The
     * modern {@code GspAutoConfiguration.gspViewResolver()} bean is
     * aliased to this name too when it fires.
     */
    public static final String TARGET_VIEW_RESOLVER_BEAN_NAME = 'jspViewResolver'

    GrailsSiteMeshViewResolverBeanPostProcessor() {
        targetViewResolverBeanName = TARGET_VIEW_RESOLVER_BEAN_NAME
        siteMeshViewResolverClass = GrailsSiteMeshViewResolver
    }

    /**
     * Contexts can include this post-processor (via {@code Sitemesh3AutoConfiguration})
     * without the SiteMesh plugin beans being registered — the unit-test context built
     * by grails-testing-support registers all Grails auto-configurations but never runs
     * the plugin's bean registrations. Decoration is impossible without those beans,
     * so leave the view resolver unwrapped instead of failing the context.
     */
    @Override
    Object postProcessAfterInitialization(Object bean, String beanName) throws BeansException {
        BeanFactory beanFactory = getBeanFactory()
        if (beanFactory == null ||
                Sitemesh3EnvironmentPostProcessor.isSiteMesh2Present() ||
                !beanFactory.containsBean(contentProcessorBeanName) ||
                !beanFactory.containsBean(decoratorSelectorBeanName)) {
            return bean
        }
        super.postProcessAfterInitialization(bean, targetNameFor(beanName, beanFactory))
    }

    /**
     * Answers with {@link #TARGET_VIEW_RESOLVER_BEAN_NAME} for a bean that carries it as an alias
     * rather than as its name, which is how {@code GspAutoConfiguration} registers the GSP view
     * resolver of a standalone Spring Boot application. The upstream implementation compares the
     * name it is handed against the target name, so an aliased resolver would never be wrapped and
     * nothing on such a page would be decorated.
     */
    private static String targetNameFor(String beanName, BeanFactory beanFactory) {
        if (beanName != TARGET_VIEW_RESOLVER_BEAN_NAME &&
                ObjectUtils.containsElement(beanFactory.getAliases(beanName), TARGET_VIEW_RESOLVER_BEAN_NAME)) {
            return TARGET_VIEW_RESOLVER_BEAN_NAME
        }
        beanName
    }

    /**
     * The upstream implementation already suppresses its zero-wrap startup
     * warning when the target resolves to a {@link org.sitemesh.webmvc.SiteMeshViewResolver}
     * (the definition-level wrap applied by
     * {@link Sitemesh3ViewResolverDefinitionPostProcessor}); the only Grails
     * addition is silence when the SiteMesh 2 module owns decoration.
     */
    @Override
    void afterSingletonsInstantiated() {
        if (Sitemesh3EnvironmentPostProcessor.isSiteMesh2Present()) {
            return
        }
        super.afterSingletonsInstantiated()
    }
}
