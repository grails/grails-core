/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.plugins.web.taglib

import java.text.Collator
import java.text.DateFormat
import java.text.DateFormatSymbols

import groovy.transform.CompileStatic
import groovy.util.logging.Slf4j

import org.springframework.beans.BeansException
import org.springframework.beans.factory.InitializingBean
import org.springframework.context.ApplicationContext
import org.springframework.context.ApplicationContextAware
import org.springframework.context.MessageSourceResolvable
import org.springframework.core.convert.ConversionService
import org.springframework.http.HttpMethod
import org.springframework.util.StringUtils
import org.springframework.web.servlet.support.RequestContextUtils as RCU
import org.springframework.web.servlet.support.RequestDataValueProcessor

import grails.artefact.TagLibrary
import grails.config.Config
import grails.core.support.GrailsConfigurationAware
import grails.gsp.TagLib
import grails.web.mapping.LinkGenerator
import org.grails.buffer.FastStringWriter
import org.grails.buffer.GrailsPrintWriter
import org.grails.core.artefact.DomainClassArtefactHandler
import org.grails.encoder.CodecLookup
import org.grails.encoder.Encoder
import org.grails.plugins.web.GrailsTagDateHelper
import org.grails.taglib.TagOutput
import org.grails.web.servlet.mvc.SynchronizerTokensHolder

/**
 * Tags for working with form controls.
 *
 * @author Graeme Rocher
 */
@TagLib
@Slf4j
class FormTagLib implements ApplicationContextAware, InitializingBean, TagLibrary, GrailsConfigurationAware {

    private static final List<String> DEFAULT_CURRENCY_CODES = ['EUR', 'XCD', 'USD', 'XOF', 'NOK', 'AUD',
                                                                'XAF', 'NZD', 'MAD', 'DKK', 'GBP', 'CHF',
                                                                'XPF', 'ILS', 'ROL', 'TRL']

    private static final List<String> DEFAULT_BOOLEAN_ATTRIBUTES = ['disabled', 'checked', 'readonly', 'required']

    ApplicationContext applicationContext
    RequestDataValueProcessor requestDataValueProcessor
    ConversionService conversionService
    GrailsTagDateHelper grailsTagDateHelper

    // Markup for localeSelect's type="dropdown", in the same spirit as ApplicationTagLib's
    // flashMessages classes: Bootstrap by default, overridable per invocation through the
    // matching attribute or app-wide by setting the property. Nothing here is emitted for
    // any other type, and a caller on another CSS framework supplies a body instead.
    String localeSelectNavItemClass = 'nav-item dropdown'
    String localeSelectToggleClass = 'nav-link dropdown-toggle'
    String localeSelectToggleIcon = 'bi bi-globe me-1'
    String localeSelectMenuClass = 'dropdown-menu dropdown-menu-end'
    String localeSelectItemClass = 'dropdown-item'
    String localeSelectActiveClass = 'active'
    String localeSelectDividerClass = 'dropdown-divider'

    CodecLookup codecLookup

    private List<String> booleanAttributes = DEFAULT_BOOLEAN_ATTRIBUTES

    // Set if Spring Security is being used and the CsrfFilter is in the Filter Chain
    Class<?> springSecurityCsrfTokenClass

    void afterPropertiesSet() {
        if (applicationContext.containsBean('requestDataValueProcessor')) {
            requestDataValueProcessor = applicationContext.getBean('requestDataValueProcessor', RequestDataValueProcessor)
        }
        if (applicationContext.containsBean('mvcConversionService')) {
            conversionService = applicationContext.getBean('mvcConversionService', ConversionService)
        }
        configureCsrf()
    }

    private void configureCsrf() {
        try {
            var filterChainProxy = applicationContext.getBean(
                    Class.forName('org.springframework.security.web.FilterChainProxy'))
            var csrfFilterClass =
                    Class.forName('org.springframework.security.web.csrf.CsrfFilter')
            if (filterChainProxy?.filterChains*.filters?.flatten()?.any { csrfFilterClass.isInstance(it) }) {
                springSecurityCsrfTokenClass =
                        Class.forName('org.springframework.security.web.csrf.CsrfToken')
            }
        } catch (ClassNotFoundException | BeansException ignore) {}
    }

    /**
     * Creates a new text field.
     *
     * @emptyTag
     *
     * @attr name REQUIRED the field name
     * @attr value the field value
     * @param name required field name
     * @param attrs optional tag attributes including value, id, class and other HTML attributes
     */
    def textField(Map attrs) {
        textFieldImpl(attrs)
    }

    private void textFieldImpl(Map attrs) {
        attrs.type = 'text'
        attrs.tagName = 'textField'
        fieldImpl(out, attrs)
    }

    /**
     * Creates a new password field.
     *
     * @emptyTag
     *
     * @attr name REQUIRED the field name
     * @attr value the field value
     * @param name required field name
     * @param attrs optional tag attributes including value, id, class and other HTML attributes
     */
    def passwordField(Map attrs) {
        passwordFieldImpl(attrs)
    }

    private void passwordFieldImpl(Map attrs) {
        attrs.type = 'password'
        attrs.tagName = 'passwordField'
        fieldImpl(out, attrs)
    }

    /**
     * Creates a hidden field.
     *
     * @attr name REQUIRED the field name
     * @attr value the field value
     * @param name required field name
     * @param attrs optional tag attributes including value and additional HTML attributes
     */
    def hiddenField(Map attrs) {
        hiddenFieldTagImpl(attrs)
    }

    private void hiddenFieldTagImpl(Map attrs) {
        hiddenFieldImpl(out, attrs)
    }

    private def hiddenFieldImpl(out, attrs) {
        attrs.type = 'hidden'
        attrs.tagName = 'hiddenField'
        fieldImpl(out, attrs)
    }

    /**
     * Creates a submit button.
     *
     * @emptyTag
     *
     * @attr name REQUIRED the field name
     * @attr value the button text
     * @attr type input type; defaults to 'submit'
     * @attr event the webflow event id
     * @param name required field name
     * @param attrs optional tag attributes including value, type, event and additional HTML attributes
     */
    def submitButton(Map attrs) {
        submitButtonImpl(attrs)
    }

    private void submitButtonImpl(Map attrs) {
        attrs.type = attrs.type ?: 'submit'
        attrs.tagName = 'submitButton'
        if (request.flowExecutionKey) {
            attrs.name = attrs.event ? "_eventId_${attrs.event}" : "_eventId_${attrs.name}"
        }
        if (attrs.name && (attrs.value == null)) {
            attrs.value = attrs.name
        }
        fieldImpl(out, attrs)
    }

    /**
     * A general tag for creating fields.
     *
     * @attr type REQUIRED the input type
     */
    def field(Map attrs) {
        attrs.tagName = 'field'
        fieldImpl(out, attrs)
    }

    @CompileStatic
    private def fieldImpl(GrailsPrintWriter out, Map attrs) {
        resolveAttributes(attrs)

        attrs.value = processFormFieldValueIfNecessary(attrs.name, attrs.value, attrs.type)

        out << "<input type=\"${attrs.remove('type')}\" "
        outputAttributes(attrs, out, true)
        out << '/>'
    }

    @CompileStatic
    private void outputNameAsIdIfIdDoesNotExist(Map attrs, GrailsPrintWriter out) {
        if (!attrs.containsKey('id') && attrs.containsKey('name')) {
            Encoder htmlEncoder = codecLookup?.lookupEncoder('HTML')
            out << 'id="'
            out << (htmlEncoder != null ? htmlEncoder.encode(attrs.name) : attrs.name)
            out << '" '
        }
    }

    /**
     * A helper tag for creating checkboxes.
     *
     * @emptyTag
     *
     * @attr name REQUIRED the name of the checkbox
     * @attr value  the value of the checkbox
     * @attr checked if evaluates to true sets to checkbox to checked
     * @attr disabled if evaluates to true sets to checkbox to disabled
     * @attr readonly if evaluates to true, sets to checkbox to read only
     * @attr id DOM element id; defaults to name
     */
    def checkBox(Map attrs) {
        def value = attrs.remove('value')
        def name = attrs.remove('name')
        def formName = attrs.get('form')

        if (!name) {
            throwTagError('Tag [checkBox] missing required attribute [name]')
        }

        booleanToAttribute(attrs, 'disabled')
        booleanToAttribute(attrs, 'readonly')

        // Deal with the "checked" attribute. If it doesn't exist, we
        // default to a value of "true", otherwise we use Groovy Truth
        // to determine whether the HTML attribute should be displayed or not.
        def checked = true
        def checkedAttributeWasSpecified = false
        if (attrs.containsKey('checked')) {
            checkedAttributeWasSpecified = true
            checked = attrs.remove('checked')
        }

        if (checked instanceof String) checked = Boolean.valueOf(checked)

        if (value == null) value = false
        def hiddenValue = ''

        def unprocessed = value
        value = processFormFieldValueIfNecessary(name, value, 'checkbox')
        hiddenValue = processFormFieldValueIfNecessary("_${name}", hiddenValue, 'hidden')

        def hiddenFieldName
        if (name.indexOf('.') == -1) {
            hiddenFieldName = "_${name}"
        } else {
            def lastDot = name.lastIndexOf('.')
            hiddenFieldName = name[0..lastDot] + '_'
            if (lastDot + 1 != name.length()) {
                hiddenFieldName += name[(lastDot + 1)..-1]
            }
        }

        out << "<input type=\"hidden\" name=\"${hiddenFieldName}\""
        if (hiddenValue != '') {
            out << " value=\"${hiddenValue}\""
        }
        if (formName) {
            out << " form=\"${formName}\""
        }
        out << " /><input type=\"checkbox\" name=\"${name}\" "
        if (checkedAttributeWasSpecified) {
            if (checked) {
                out << 'checked="checked" '
            }
        }
        else if (unprocessed) {
            out << 'checked="checked" '
        }

        def outputValue = !(unprocessed instanceof Boolean || unprocessed?.getClass() == boolean)
        if (outputValue) {
            out << "value=\"${value}\" "
        }
        // process remaining attributes
        outputAttributes(attrs, out)

        if (!attrs.containsKey('id')) {
            out << """id="${name}" """
        }

        // close the tag, with no body
        out << ' />'
    }

    /**
     * A general tag for creating textareas.
     *
     * @attr name REQUIRED the name of the textarea
     * @attr value  the text of the textarea; if not specified renders the body as the text
     * @attr escapeHtml if true escapes the text as HTML
     * @attr id DOM element id; defaults to name
     */
    def textArea(Map attrs, Closure body) {
        resolveAttributes(attrs)
        // Pull out the value to use as content not attrib
        def value = attrs.remove('value')
        if (!value) {
            value = body()
        }

        boolean escapeHtml = true
        if (attrs.containsKey('escapeHtml')) {
            escapeHtml = attrs.boolean('escapeHtml')
            attrs.remove('escapeHtml')
        }

        // Add textarea field to requestDataValueProcessor
        def content = (escapeHtml ? value.encodeAsHTML() : value)
        if (attrs.name) {
            content = processFormFieldValueIfNecessary(attrs.name, content, 'textarea')
        }
        out << '<textarea '
        outputAttributes(attrs, out, true)
        out << '>' << content << '</textarea>'
    }

    /**
     * Some attributes can be defined as Boolean values, but the html specification
     * mandates the attribute must have the same value as its name. For example,
     * disabled, readonly and checked.
     */
    @CompileStatic
    private void booleanToAttribute(Map attrs, String attrName) {
        def attrValue = attrs.remove(attrName)
        if (attrValue instanceof CharSequence) {
            attrValue = attrValue.toString().trim()
        }
        // If the value is the same as the name or if it is a boolean value,
        // reintroduce the attribute to the map according to the w3c rules, so it is output later
        if ((attrValue instanceof Boolean && attrValue) ||
            (attrValue instanceof String && (((String) attrValue).equalsIgnoreCase('true') || ((String) attrValue).equalsIgnoreCase(attrName)))) {
            attrs.put(attrName, attrName)
        } else if (attrValue instanceof String && !((String) attrValue).equalsIgnoreCase('false')) {
            // If the value is not the string 'false', then we should just pass it on to
            // keep compatibility with existing code
            attrs.put(attrName, attrValue)
        }
    }

    /**
     * Check required attributes, set the id to name if no id supplied, extract bean values etc.
     */
    private void resolveAttributes(Map attrs) {
        if (!attrs.name && !attrs.field) {
            throwTagError("Tag [${attrs.tagName}] is missing required attribute [name] or [field]")
        }

        attrs.remove('tagName')

        def val = attrs.remove('bean')
        if (val) {
            if (attrs.name.indexOf('.')) {
                attrs.name.split('\\.').each { val = val?."$it" }
            }
            else {
                val = val[name]
            }
            attrs.value = val
        }
        attrs.value = attrs.value != null ? attrs.value : '' // can't use ?: since 0 is groovy false

        booleanAttributes.each {
            booleanToAttribute(attrs, it)
        }
    }

    /**
     * Dump out attributes in HTML compliant fashion.
     */
    @CompileStatic
    private void outputAttributes(Map attrs, GrailsPrintWriter writer, boolean useNameAsIdIfIdDoesNotExist = false) {
        attrs.remove('tagName') // Just in case one is left
        Encoder htmlEncoder = codecLookup?.lookupEncoder('HTML')
        attrs.each { k, v ->
            if (v != null) {
                writer << k
                writer << '="'
                writer << (htmlEncoder != null ? htmlEncoder.encode(v) : v)
                writer << '" '
            }
        }
        if (useNameAsIdIfIdDoesNotExist) {
            outputNameAsIdIfIdDoesNotExist(attrs, writer)
        }
    }

    /**
     * Same as &lt;g:form&gt;, except sets the relevant enctype for a file upload form.
     *
     * @attr action the name of the action to use in the link, if not specified the default action will be linked
     * @attr controller the name of the controller to use in the link, if not specified the current controller will be linked
     * @attr id The id to use in the link
     * @attr url A map containing the action, controller, id etc.
     * @attr name A value to use for both the name and id attribute of the form tag
     * @attr useToken Set whether to send a token in the request to handle duplicate form submissions. See Handling Duplicate Form Submissions
     * @attr method the form method to use, either 'POST' or 'GET'; defaults to 'POST'
     */
    def uploadForm(Map attrs, Closure body) {
        attrs.enctype = 'multipart/form-data'
        form(attrs, body)
    }

    /**
     * General linking to controllers, actions etc. Examples:<br/>
     *
     * &lt;g:form action="myaction"&gt;...&lt;/g:form&gt;<br/>
     * &lt;g:form controller="myctrl" action="myaction"&gt;...&lt;/g:form&gt;<br/>
     *
     * @attr action the name of the action to use in the link, if not specified the default action will be linked
     * @attr controller the name of the controller to use in the link, if not specified the current controller will be linked
     * @attr id The id to use in the link
     * @attr url A map containing the action, controller, id etc.
     * @attr name A value to use for both the name and id attribute of the form tag
     * @attr useToken Set whether to send a token in the request to handle duplicate form submissions. See Handling Duplicate Form Submissions
     * @attr method the form method to use, either 'POST' or 'GET'; defaults to 'POST'
     */
    def form(Map attrs, Closure body) {

        boolean useToken = false
        if (attrs.containsKey('useToken')) {
            useToken = attrs.boolean('useToken')
            attrs.remove('useToken')
        }

        def writer = getOut()

        def linkAttrs = attrs.subMap(LinkGenerator.LINK_ATTRIBUTES)

        writer << '<form action="'

        // Call RequestDataValueProcessor to modify url if necessary
        def link = createLink(linkAttrs)
        if (requestDataValueProcessor != null) {
            link = requestDataValueProcessor.processAction(request, link, request.method)
        }

        writer << link
        writer << '" '

        // if URL is not null remove attributes
        if (attrs.url == null) {
            attrs = attrs - linkAttrs
        }
        else {
            attrs.remove('url')
        }

        // default to post
        def method
        if (linkAttrs[LinkGenerator.ATTRIBUTE_RESOURCE] && linkAttrs[LinkGenerator.ATTRIBUTE_ACTION]) {
            method = LinkGenerator.REST_RESOURCE_ACTION_TO_HTTP_METHOD_MAP.get(linkAttrs[LinkGenerator.ATTRIBUTE_ACTION].toString())
        } else {
            method = linkAttrs[LinkGenerator.ATTRIBUTE_METHOD]?.toUpperCase() ?: 'POST'
        }
        def httpMethod = method != null ? HttpMethod.valueOf(method) : HttpMethod.POST
        boolean notGet = httpMethod != HttpMethod.GET

        if (notGet) {
            writer << 'method="post" '
        } else {
            writer << 'method="get" '
        }

        attrs.remove('method')
        // process remaining attributes
        if (attrs.id == null) attrs.remove('id')

        outputAttributes(attrs, writer, true)

        writer << '>'
        if (request['flowExecutionKey']) {
            writer.println()
            hiddenFieldImpl(writer, [name: 'execution', value: request['flowExecutionKey']])
        }

        // A browser submits only GET or POST, so any other method travels as this parameter - read by the
        // servlet filter in one mode and by the dispatcher in the other. The POST route on a resources
        // member URL is a fallback for clients that cannot send it, not a replacement: it reaches update
        // alone, and covers neither a singular resource nor a URL an application mapped to PUT itself.
        if (notGet && httpMethod != HttpMethod.POST) {
            hiddenFieldImpl(writer, [name: '_method', value: httpMethod.toString()])
        }
        if (notGet && springSecurityCsrfTokenClass) {
            var csrfToken = request[springSecurityCsrfTokenClass.getName()]
            if (csrfToken) {
                hiddenFieldImpl(writer, [name: csrfToken.parameterName, value: csrfToken.token])
            }
        }

        if (useToken) {
            def tokensHolder = SynchronizerTokensHolder.store(session)
            writer.println()
            hiddenFieldImpl(writer, [name: SynchronizerTokensHolder.TOKEN_KEY, value: tokensHolder.generateToken(request.forwardURI)])
            writer.println()
            hiddenFieldImpl(writer, [name: SynchronizerTokensHolder.TOKEN_URI, value: request.forwardURI])
        }

        // output the body
        writer << body()

        //Write RequestDataValueProcessor hidden fields if necessary
        if (requestDataValueProcessor != null) {
            writeHiddenFields(requestDataValueProcessor.getExtraHiddenFields(request))
        }
        // close tag
        writer << '</form>'
    }

    /**
     * generate hidden inputs
     */
    private void writeHiddenFields(hiddenFields) {
        def writer = getOut()
        hiddenFields.each { key, value -> writer << "<input type=\"hidden\" name=\"${key}\" value=\"${value}\" />\n" }
    }

    /**
     * Creates a submit button using the `formaction` attribute to submit to a different action than the form.
     * The target URL is generated from the supported link attributes.<br/>
     * The rendered `&lt;input&gt;` uses this tag's `id` attribute for its DOM id. If the generated URL needs an
     * `id`, provide it through the `url` map.<br/>
     *
     * &lt;g:formActionSubmit action="myaction" value="Submit"/&gt;<br/>
     * &lt;g:formActionSubmit controller="myctrl" action="myaction" value="ButtonName"/&gt;<br/>
     *
     * @emptyTag
     *
     * @attr value REQUIRED The label shown on the submit button
     * @attr id DOM id for the rendered input element
     * @attr disabled Makes the button disabled. Will be interpreted as a Groovy Truth
     * @attr action The name of the action to use in the generated link, if not specified the default action will be linked
     * @attr controller The name of the controller to use in the generated link, if not specified the current controller will be linked
     * @attr namespace The namespace of the controller to use in the generated link
     * @attr plugin The name of the plugin which provides the controller
     * @attr fragment The link fragment (often called anchor tag) to use
     * @attr mapping The named URL mapping to use to rewrite the link
     * @attr method The HTTP method specified in the corresponding URL mapping
     * @attr params A map containing URL query parameters for the link
     * @attr url A map containing the action, controller, id etc. for the generated link
     * @attr uri A string for a relative path in the running app.
     * @attr relativeUri Used to specify a uri relative to the current path.
     * @attr absolute If set to "true" will prefix the link target address with the value of the grails.serverURL property from Config, or http://localhost:&lt;port&gt; if no value in Config and not running in production.
     * @attr base Sets the prefix to be added to the link target address, typically an absolute server URL. This overrides the behaviour of the absolute property, if both are specified.
     * @attr event Webflow _eventId parameter
     */
    def formActionSubmit(Map attrs) {
        if (!attrs.value) {
            throwTagError('Tag [formActionSubmit] is missing required attribute [value]')
        }

        def elementId = attrs.remove('id')

        // the following attributes are reserved because this tag must be of type `submit` and the `formaction` attr
        // will be generated by the link attributes.
        attrs.remove('type')
        attrs.remove('formAction')

        out << '<input type="submit" formaction="'

        Map linkAttrs = attrs.subMap(LinkGenerator.LINK_ATTRIBUTES - 'elementId')

        // Call RequestDataValueProcessor to modify url if necessary
        String link = createLink(linkAttrs)
        if (requestDataValueProcessor != null) {
            link = requestDataValueProcessor.processAction(request, link, request.method)
        }

        out << link
        out << '" '

        attrs.keySet().removeAll(LinkGenerator.LINK_ATTRIBUTES)
        if (elementId) {
            attrs['id'] = elementId
        }

        booleanToAttribute(attrs, 'disabled')

        // process remaining attributes
        outputAttributes(attrs, out, false)

        // close tag
        out << '/>'
    }

    /**
     * Creates a an image submit button that submits to an action in the controller specified by the form action.
     * The name of the action attribute is translated into the action name, for example "Edit" becomes
     * "_action_edit" or "List People" becomes "_action_listPeople".<br/>
     * If the action attribute is not specified, the value attribute will be used as part of the action name.<br/>
     *
     * &lt;g:actionSubmitImage src="/images/submitButton.gif" action="Edit" /&gt;
     *
     * @emptyTag
     *
     * @attr value REQUIRED The title of the button and name of action when not explicitly defined.
     * @attr action The name of the action to be executed, otherwise it is derived from the value.
     * @attr src The source of the image to use
     * @attr disabled Makes the button to be disabled. Will be interpreted as a Groovy Truth
     */
    def actionSubmitImage(Map attrs) {
        attrs.tagName = 'actionSubmitImage'

        if (!attrs.value) {
            throwTagError("Tag [$attrs.tagName] is missing required attribute [value]")
        }

        // add action and value
        def value = attrs.remove('value')
        def action = attrs.remove('action') ?: value
        //Change this button to use requestDataValueProcessor
        value = processFormFieldValueIfNecessary("_action_${action}", "${value}", 'image')
        booleanToAttribute(attrs, 'disabled')

        out << "<input type=\"image\" name=\"_action_${action}\" value=\"${value}\" "

        // add image src
        def src = attrs.remove('src')
        if (src) {
            src = processedUrl(src, request)
            out << "src=\"${src}\" "
        }

        // process remaining attributes
        outputAttributes(attrs, out)

        // close tag
        out << '/>'
    }

    /**
     * A simple date picker that renders a date as selects.<br/>
     * e.g. &lt;g:datePicker name="myDate" value="${new Date()}" /&gt;
     *
     * @emptyTag
     *
     * @attr name REQUIRED The name of the date picker field set
     * @attr value The current value of the date picker; defaults to either the value specified by the default attribute or now if no default is set
     * @attr default A Date or parsable date string that will be used if there is no value
     * @attr precision The desired granularity of the date to be rendered
     * @attr noSelection A single-entry map detailing the key and value to use for the "no selection made" choice in the select box. If there is no current selection this will be shown as it is first in the list, and if submitted with this selected, the key that you provide will be submitted. Typically this will be blank.
     * @attr years A list or range of years to display, in the order specified. i.e. specify 2007..1900 for a reverse order list going back to 1900. If this attribute is not specified, a range of years from the current year - 100 to current year + 100 will be shown.
     * @attr relativeYears A range of int representing values relative to value. For example, a relativeYears of -2..7 and a value of today will render a list of 10 years starting with 2 years ago through 7 years in the future. This can be useful for things like credit card expiration dates or birthdates which should be bound relative to today.
     * @attr id the DOM element id
     * @attr disabled Makes the resulting inputs and selects to be disabled. Is treated as a Groovy Truth.
     * @attr readonly Makes the resulting inputs and selects to be made read only. Is treated as a Groovy Truth.
     * @attr locale The locale to use for display formatting. Defaults to the current request locale and then the system default locale if not specified.
     * @attr selectDateClass css class added to each select tag
     */
    def datePicker(Map attrs) {
        def out = out // let x = x ?
        def xdefault = attrs['default']
        if (xdefault == null) {
            xdefault = new Date()
        }
        else if (xdefault.toString() != 'none') {
            if (xdefault instanceof String) {
                xdefault = DateFormat.getInstance().parse(xdefault)

            }
            else if (!grailsTagDateHelper.supportsDatePicker(xdefault.class)) {
                throwTagError('Tag [datePicker] the default date is not a supported class')
            }
        }
        else {
            xdefault = null
        }
        def years = attrs.years
        def relativeYears = attrs.relativeYears
        if (years != null && relativeYears != null) {
            throwTagError('Tag [datePicker] does not allow both the years and relativeYears attributes to be used together.')
        }

        if (relativeYears != null) {
            if (!(relativeYears instanceof IntRange)) {
                // allow for a syntax like relativeYears="[-2..5]".  The value there is a List containing an IntRage.
                if ((!(relativeYears instanceof List)) || (relativeYears.size() != 1) || (!(relativeYears[0] instanceof IntRange))) {
                    throwTagError('The [datePicker] relativeYears attribute must be a range of int.')
                }
                relativeYears = relativeYears[0]
            }
        }
        def value = attrs.value
        if (value.toString() == 'none') {
            value = null
        }
        else if (!value) {
            value = xdefault
        }
        def name = attrs.name
        def id = attrs.id ?: name

        def noSelection = attrs.noSelection
        if (noSelection != null) {
            noSelection = noSelection.entrySet().iterator().next()
        }

        final PRECISION_RANKINGS = ['year': 0, 'month': 10, 'day': 20, 'hour': 30, 'minute': 40]
        def precision = PRECISION_RANKINGS[
            attrs.precision ?:
            grailsApplication.config.getProperty('grails.tags.datePicker.default.precision', 'minute')
        ]

        def day
        def month
        def year
        def hour
        def minute
        def dfs = new DateFormatSymbols(FormatTagLib.resolveLocale(attrs.remove('locale')))

        def c = null
        if (value instanceof Calendar) {
            c = value
        }
        else if (value != null) {
            c = grailsTagDateHelper.buildCalendar(value)
        }

        if (c != null) {
            day = c.get(GregorianCalendar.DAY_OF_MONTH)
            month = c.get(GregorianCalendar.MONTH)
            year = c.get(GregorianCalendar.YEAR)
            hour = c.get(GregorianCalendar.HOUR_OF_DAY)
            minute = c.get(GregorianCalendar.MINUTE)
        }

        if (years == null) {
            def tempyear
            if (year == null) {
                // If no year, we need to get current year to setup a default range... ugly
                def tempc = new GregorianCalendar()
                tempc.setTime(new Date())
                tempyear = tempc.get(GregorianCalendar.YEAR)
            }
            else {
                tempyear = year
            }
            if (relativeYears) {
                if (relativeYears.reverse) {
                    years = (tempyear + relativeYears.toInt)..(tempyear + relativeYears.fromInt)
                } else {
                    years = (tempyear + relativeYears.fromInt)..(tempyear + relativeYears.toInt)
                }
            } else {
                years = (tempyear + 100)..(tempyear - 100)
            }
        }

        booleanToAttribute(attrs, 'disabled')
        booleanToAttribute(attrs, 'readonly')
        out.println("<label style=\"display:none;\" for=\"${name}_day\" id=\"label_${name}_day\">Day</label>")
        out.println("<label style=\"display:none;\" for=\"${name}_month\" id=\"label_${name}_month\">Month</label>")
        out.println("<label style=\"display:none;\" for=\"${name}_year\" id=\"label_${name}_year\">Year</label>")
        out.println("<label style=\"display:none;\" for=\"${name}_hour\" id=\"label_${name}_hour\">Hour</label>")
        out.println("<label style=\"display:none;\" for=\"${name}_minute\" id=\"label_${name}_minute\">Minute</label>")
        // Change this hidden to use requestDataValueProcessor
        def dateStructValue = processFormFieldValueIfNecessary("${name}", 'date.struct', 'hidden')
        out.println("<input type=\"hidden\" name=\"${name}\" value=\"${dateStructValue}\" />")

        // create day select
        if (precision >= PRECISION_RANKINGS['day']) {
            out.println("<select name=\"${name}_day\" id=\"${id}_day\" aria-labelledby=\"${name} ${name}_day\"")
            if (attrs.disabled) {
                out << ' disabled="disabled"'
            }
            if (attrs.readonly) {
                out << ' readonly="readonly"'
            }
            if (attrs.selectDateClass) {
                out << ' class="' + attrs.selectDateClass + '"'
            }
            out << '>'

            if (noSelection) {
                renderNoSelectionOptionImpl(out, noSelection.key, noSelection.value, '')
                out.println()
            }

            for (i in 1..31) {
                // Change this option to use requestDataValueProcessor
                def dayIndex = processFormFieldValueIfNecessary("${name}_day", "${i}", 'option')
                out.println("<option value=\"${dayIndex}\"${i == day ? ' selected="selected"' : ''}>${i}</option>")
            }
            out.println('</select>')
        }

        // create month select
        if (precision >= PRECISION_RANKINGS['month']) {
            out.println("<select name=\"${name}_month\" id=\"${id}_month\" aria-labelledby=\"${name} ${name}_month\"")
            if (attrs.disabled) {
                out << ' disabled="disabled"'
            }
            if (attrs.readonly) {
                out << ' readonly="readonly"'
            }
            if (attrs.selectDateClass) {
                out << ' class="' + attrs.selectDateClass + '"'
            }
            out << '>'

            if (noSelection) {
                renderNoSelectionOptionImpl(out, noSelection.key, noSelection.value, '')
                out.println()
            }

            dfs.months.eachWithIndex { m, i ->
                if (m) {
                    def monthIndex = i + 1
                    monthIndex = processFormFieldValueIfNecessary("${name}_month", "${monthIndex}", 'option')
                    out.println("<option value=\"${monthIndex}\"${i == month ? ' selected="selected"' : ''}>$m</option>")
                }
            }
            out.println('</select>')
        }

        // create year select
        if (precision >= PRECISION_RANKINGS['year']) {
            out.println("<select name=\"${name}_year\" id=\"${id}_year\" aria-labelledby=\"${name} ${name}_year\"")
            if (attrs.disabled) {
                out << ' disabled="disabled"'
            }
            if (attrs.readonly) {
                out << ' readonly="readonly"'
            }
            if (attrs.selectDateClass) {
                out << ' class="' + attrs.selectDateClass + '"'
            }
            out << '>'

            if (noSelection) {
                renderNoSelectionOptionImpl(out, noSelection.key, noSelection.value, '')
                out.println()
            }

            for (i in years) {
                // Change this year option to use requestDataValueProcessor
                def yearIndex  = processFormFieldValueIfNecessary("${name}_year", "${i}", 'option')
                out.println("<option value=\"${yearIndex}\"${i == year ? ' selected="selected"' : ''}>${i}</option>")
            }
            out.println('</select>')
        }

        // do hour select
        if (precision >= PRECISION_RANKINGS['hour']) {
            out.println("<select name=\"${name}_hour\" id=\"${id}_hour\" aria-labelledby=\"${name} ${name}_hour\"")
            if (attrs.disabled) {
                out << ' disabled="disabled"'
            }
            if (attrs.readonly) {
                out << ' readonly="readonly"'
            }
            if (attrs.selectDateClass) {
                out << ' class="' + attrs.selectDateClass + '"'
            }
            out << '>'

            if (noSelection) {
                renderNoSelectionOptionImpl(out, noSelection.key, noSelection.value, '')
                out.println()
            }

            for (i in 0..23) {
                def h = '' + i
                if (i < 10) h = '0' + h
                // This option add hour to requestDataValueProcessor
                h  = processFormFieldValueIfNecessary("${name}_hour", "${h}", 'option')
                out.println("<option value=\"${h}\"${i == hour ? ' selected="selected"' : ''}>$h</option>")
            }
            out.println('</select> :')

            // If we're rendering the hour, but not the minutes, then display the minutes as 00 in read-only format
            if (precision < PRECISION_RANKINGS['minute']) {
                out.println('00')
            }
        }

        // do minute select
        if (precision >= PRECISION_RANKINGS['minute']) {
            out.println("<select name=\"${name}_minute\" id=\"${id}_minute\" aria-labelledby=\"${name} ${name}_minute\"")
            if (attrs.disabled) {
                out << 'disabled="disabled"'
            }
            if (attrs.readonly) {
                out << 'readonly="readonly"'
            }
            if (attrs.selectDateClass) {
                out << ' class="' + attrs.selectDateClass + '"'
            }
            out << '>'

            if (noSelection) {
                renderNoSelectionOptionImpl(out, noSelection.key, noSelection.value, '')
                out.println()
            }

            for (i in 0..59) {
                def m = '' + i
                if (i < 10) m = '0' + m
                m  = processFormFieldValueIfNecessary("${name}_minute", "${m}", 'option')
                out.println("<option value=\"${m}\"${i == minute ? ' selected="selected"' : ''}>$m</option>")
            }
            out.println('</select>')
        }
    }

    private def renderNoSelectionOption(noSelectionKey, noSelectionValue, value) {
        renderNoSelectionOptionImpl(out, noSelectionKey, noSelectionValue, value)
    }

    private def renderNoSelectionOptionImpl(out, noSelectionKey, noSelectionValue, value) {
        // If a label for the '--Please choose--' first item is supplied, write it out
        out << "<option value=\"${(noSelectionKey == null ? '' : noSelectionKey)}\"${noSelectionKey == value ? ' selected="selected"' : ''}>${noSelectionValue.encodeAsHTML()}</option>"
    }

    /**
     * A helper tag for creating TimeZone selects.<br/>
     * eg. &lt;g:timeZoneSelect name="myTimeZone" value="${tz}" /&gt;
     *
     * @emptyTag
     *
     * @attr name REQUIRED The name of the select
     * @attr value An instance of java.util.TimeZone. Defaults to the time zone for the current Locale if not specified
     * @attr locale The locale to use for formatting the time zone names. Defaults to the current request locale and then system default locale if not specified
     */
    def timeZoneSelect(Map attrs) {
        attrs.from = TimeZone.getAvailableIDs()
        attrs.value = (attrs.value ? attrs.value.ID : TimeZone.getDefault().ID)
        def date = new Date()
        def locale = FormatTagLib.resolveLocale(attrs.locale)

        // set the option value as a closure that formats the TimeZone for display
        attrs.optionValue = {
            TimeZone tz = TimeZone.getTimeZone(it)
            def shortName = tz.getDisplayName(tz.inDaylightTime(date), TimeZone.SHORT, locale)
            def longName = tz.getDisplayName(tz.inDaylightTime(date), TimeZone.LONG, locale)

            def offset = tz.rawOffset
            def hour = offset / (60 * 60 * 1000)
            def min = Math.abs(offset / (60 * 1000)) % 60

            return "${shortName}, ${longName} ${hour}:${min} [${it}]"
        }

        // use generic select
        select(attrs)
    }

    /**
     * A helper tag for locale selection.<br/>
     *
     * <p>With no body it renders a control: a native {@code <select>} by default, or a plain list of
     * {@code <a>} links with {@code type="links"}. With a body it becomes an iterating tag &mdash; it
     * resolves the locales once and renders the body for each, exposing a per-locale model under the
     * {@code var} attribute so the caller supplies its own markup (a Bootstrap dropdown, a footer
     * list, etc.). The model exposes: {@code locale}, {@code tag} (BCP&#8209;47), {@code code}
     * ({@code language_COUNTRY}), {@code autonym} (the name in its own language, in CLDR's
     * mid-sentence form), {@code menuName} (that same name titlecased for standalone display,
     * per CLDR's uiListOrMenu context transform &mdash; what a language menu wants),
     * {@code name} (the name in the display locale), {@code label}, {@code active} (matches the
     * current locale), {@code default} (matches the configured default) and {@code index}.
     *
     * eg. &lt;g:localeSelect name="myLocale" value="${locale}" labelType="autonym" /&gt;
     *
     * @attr name The name of the select (select mode)
     * @attr value The selected locale, defaults to the current request locale if not specified
     * @attr available If <code>true</code>, list only the locales the application is translated into
     * (those with a <code>messages_*.properties</code> bundle, as published to the servlet context by
     * the i18n plugin) instead of every locale the JVM knows about. Defaults to <code>false</code>.
     * @attr type <code>select</code> (default), <code>links</code>, or <code>dropdown</code> for a
     * ready-made Bootstrap navbar language menu needing no body. Ignored when a body is supplied.
     * @attr labelType The option/link label: <code>autonym</code> (each locale in its own language),
     * <code>name</code> (in the display locale), <code>both</code>, or omitted for the legacy
     * <code>"language, [COUNTRY,] name"</code> label.
     * @attr sort If <code>true</code>, order the locales by their label using a locale-independent collator.
     * @attr tags If <code>true</code>, option/link keys are BCP&#8209;47 language tags (<code>en-US</code>)
     * rather than the legacy <code>en_US</code> form.
     * @attr pinDefault If <code>true</code> (body mode), the configured default locale is emitted first.
     * @attr param The request parameter name used for <code>links</code>-mode hrefs. Defaults to <code>lang</code>.
     * @attr var Enables body mode: the name of the per-locale model variable exposed to the body.
     * Falls back to <code>type</code> when no body is actually supplied.
     * @attr id <code>dropdown</code> only: id of the toggle, referenced by the menu's
     * <code>aria-labelledby</code>. Defaults to <code>localeDropdown</code>.
     * @attr navItemClass <code>dropdown</code> only: class of the wrapping <code>li</code>
     * (default: <code>nav-item dropdown</code>).
     * @attr toggleClass <code>dropdown</code> only: class of the toggle link
     * (default: <code>nav-link dropdown-toggle</code>).
     * @attr icon <code>dropdown</code> only: icon class rendered before the toggle label
     * (default: <code>bi bi-globe me-1</code>). Pass an empty string for no icon.
     * @attr menuClass <code>dropdown</code> only: class of the menu <code>ul</code>
     * (default: <code>dropdown-menu dropdown-menu-end</code>).
     * @attr itemClass <code>dropdown</code> only: class of each entry (default: <code>dropdown-item</code>).
     * @attr activeClass <code>dropdown</code> only: class added to the current locale's entry
     * (default: <code>active</code>).
     * @attr dividerClass <code>dropdown</code> only: class of the rule after a pinned default
     * (default: <code>dropdown-divider</code>).
     */
    def localeSelect(Map attrs, Closure body) {
        boolean availableOnly = Boolean.valueOf(attrs.remove('available')?.toString())
        List locales
        if (availableOnly) {
            def published = request.servletContext?.getAttribute('availableLocales')
            locales = published ? new ArrayList(published) : [RCU.getLocale(request)]
        }
        else {
            locales = Locale.getAvailableLocales() as List
        }

        Locale current = RCU.getLocale(request)
        def valueAttr = attrs.value
        if (valueAttr instanceof Locale) {
            current = valueAttr
        }
        else if (valueAttr) {
            current = StringUtils.parseLocale(valueAttr.toString()) ?: current
        }

        boolean useTags = Boolean.valueOf(attrs.remove('tags')?.toString())
        Closure label = localeLabel(attrs.remove('labelType'), current)

        if (Boolean.valueOf(attrs.remove('sort')?.toString())) {
            Collator collator = Collator.getInstance(Locale.ROOT)
            locales = locales.sort(false) { a, b -> collator.compare(label(a).toString(), label(b).toString()) }
        }

        String varName = attrs.remove('var')
        String type = (attrs.remove('type') ?: 'select').toString()
        boolean pinDefault = Boolean.valueOf(attrs.remove('pinDefault')?.toString())
        // A body stays optional even with var: GSP hands an absent body in as
        // TagOutput.EMPTY_BODY_CLOSURE, and iterating over that would silently emit one
        // empty string per locale, so fall through to the requested type instead.
        boolean iterate = varName && body != null && body != TagOutput.EMPTY_BODY_CLOSURE

        if (iterate || type == 'dropdown') {
            Locale defaultLocale = configuredDefaultLocale()
            // Lists can carry several country variants of one language with no bare-language
            // entry (pt_BR/pt_PT, zh_CN/zh_TW), so active and default must each elect a
            // SINGLE winner: the exact match, else the bare-language entry, else the first
            // locale sharing the language. Pinning keeps the losing variants in the list.
            Locale defaultEntry = singleWinner(locales, defaultLocale)
            boolean pinned = pinDefault && defaultEntry != null
            if (pinned) {
                locales = [defaultEntry] + locales.findAll { !it.is(defaultEntry) }
            }
            Locale activeEntry = singleWinner(locales, current)

            if (iterate) {
                locales.eachWithIndex { locale, i ->
                    out << body([(varName): [
                            locale: locale,
                            tag: locale.toLanguageTag(),
                            code: localeKey(locale),
                            autonym: locale.getDisplayName(locale),
                            menuName: menuCase(locale.getDisplayName(locale), locale),
                            name: locale.getDisplayName(current),
                            label: label(locale),
                            active: locale.is(activeEntry),
                            'default': locale.is(defaultEntry),
                            index: i
                    ]])
                }
                return
            }

            renderLocaleDropdown(attrs, locales, current, activeEntry, pinned)
            return
        }

        if (type == 'links') {
            String param = attrs.remove('param') ?: 'lang'
            attrs.remove('name')
            def styleClass = attrs.remove('class')
            String classAttr = styleClass ? " class=\"${styleClass.toString().encodeAsHTML()}\"" : ''
            locales.each { locale ->
                String key = useTags ? locale.toLanguageTag() : localeKey(locale)
                out << "<a href=\"${localeHref(param, key).encodeAsHTML()}\"${classAttr}>${label(locale).toString().encodeAsHTML()}</a>"
            }
            return
        }

        // select mode
        attrs.remove('param')
        attrs.from = locales
        attrs.value = useTags ? current.toLanguageTag() : localeKey(current)
        attrs.optionKey = useTags ? { it.toLanguageTag() } : { localeKey(it) }
        attrs.optionValue = label
        out << select(attrs)
    }

    private static String localeKey(Locale locale) {
        locale.country ? "${locale.language}_${locale.country}" : locale.language
    }

    /**
     * A query-only href that switches the language and keeps everything else.
     *
     * A bare {@code ?lang=de} replaces the whole query component, so a visitor changing
     * language on {@code /books?page=2&sort=title} would land on {@code /books?lang=de}
     * with the paging and sorting silently discarded. The current query string is carried
     * over, with any existing value for this parameter dropped rather than duplicated.
     *
     * Reading {@code request.queryString} rather than {@code params} is deliberate: params
     * also holds values bound from the URL path by the mappings (an {@code id} in
     * {@code /book/show/5}) and the controller and action names, none of which belong in a
     * query string. The path itself needs no handling, since a query-only href resolves
     * against the current URL.
     */
    private String localeHref(String param, String key) {
        StringBuilder href = new StringBuilder('?')
        String queryString = request.queryString
        if (queryString) {
            queryString.tokenize('&').each { String pair ->
                int eq = pair.indexOf('=')
                String name = eq == -1 ? pair : pair.substring(0, eq)
                if (URLDecoder.decode(name, 'UTF-8') == param) {
                    return
                }
                if (href.length() > 1) {
                    href << '&'
                }
                href << pair
            }
        }
        if (href.length() > 1) {
            href << '&'
        }
        href << URLEncoder.encode(param, 'UTF-8') << '=' << URLEncoder.encode(key, 'UTF-8')
        href.toString()
    }

    /**
     * CLDR stores a language name in its mid-sentence form, so languages that do not
     * capitalize their own name yield "espa&ntilde;ol" or "&#1088;&#1091;&#1089;&#1089;&#1082;&#1080;&#1081;". A menu is not a
     * sentence: CLDR's uiListOrMenu context transform calls for titlecase-firstword,
     * which {@code Locale.getDisplayName} never applies. Uppercase with the locale's OWN
     * casing rules rather than the JVM default, so Turkish and Azeri get the dotted
     * &#304;; caseless scripts and already-capitalized names pass through unchanged.
     */
    private static String menuCase(String name, Locale locale) {
        name ? name.substring(0, 1).toUpperCase(locale) + name.substring(1) : name
    }

    private void renderLocaleDropdown(Map attrs, List locales, Locale current, Locale activeEntry, boolean pinned) {
        // A single-language application has nothing to switch between, so it should not
        // carry a language menu at all. Callers get this for free instead of guarding.
        if (locales.size() < 2) {
            return
        }

        String param = attrs.param ?: 'lang'
        String id = attrs.id ?: 'localeDropdown'
        String navItemClass = attrs.navItemClass ?: localeSelectNavItemClass
        String toggleClass = attrs.toggleClass ?: localeSelectToggleClass
        // Distinguish "not supplied" from icon="" so the icon can be switched off.
        String icon = attrs.icon != null ? attrs.icon.toString() : localeSelectToggleIcon
        String menuClass = attrs.menuClass ?: localeSelectMenuClass
        String itemClass = attrs.itemClass ?: localeSelectItemClass
        String activeClass = attrs.activeClass ?: localeSelectActiveClass
        String dividerClass = attrs.dividerClass ?: localeSelectDividerClass

        out << "<li class=\"${navItemClass.encodeAsHTML()}\">"
        out << "<a class=\"${toggleClass.encodeAsHTML()}\" href=\"#\" id=\"${id.encodeAsHTML()}\" " +
                'role="button" data-bs-toggle="dropdown" aria-expanded="false">'
        if (icon) {
            out << "<i class=\"${icon.encodeAsHTML()}\"></i>"
        }
        out << menuCase(current.getDisplayName(current), current).encodeAsHTML()
        out << '</a>'
        out << "<ul class=\"${menuClass.encodeAsHTML()}\" aria-labelledby=\"${id.encodeAsHTML()}\">"
        locales.eachWithIndex { locale, i ->
            // The pinned default stands alone above the divider, so a visitor who switched
            // to a language they cannot read always has a recognizable way back at the top.
            if (pinned && i == 1) {
                out << "<li><hr class=\"${dividerClass.encodeAsHTML()}\"></li>"
            }
            String cssClass = locale.is(activeEntry) ? "${itemClass} ${activeClass}" : itemClass
            out << "<li><a class=\"${cssClass.encodeAsHTML()}\" " +
                    "href=\"${localeHref(param, locale.toLanguageTag()).encodeAsHTML()}\">" +
                    "${menuCase(locale.getDisplayName(locale), locale).encodeAsHTML()}</a></li>"
        }
        out << '</ul></li>'
    }

    private static Locale singleWinner(List locales, Locale wanted) {
        locales.find { it.language == wanted.language && it.country == wanted.country } ?:
                locales.find { it.language == wanted.language && !it.country } ?:
                locales.find { it.language == wanted.language }
    }

    private Closure localeLabel(labelType, Locale display) {
        switch (labelType) {
            case 'autonym':
                return { Locale locale -> locale.getDisplayName(locale) }
            case 'name':
                return { Locale locale -> locale.getDisplayName(display) }
            case 'both':
                return { Locale locale ->
                    String autonym = locale.getDisplayName(locale)
                    String name = locale.getDisplayName(display)
                    autonym == name ? autonym : "${autonym} — ${name}"
                }
            default:
                return { Locale locale ->
                    locale.country ? "${locale.language}, ${locale.country},  ${locale.displayName}" : "${locale.language}, ${locale.displayName}"
                }
        }
    }

    private Locale configuredDefaultLocale() {
        def configured = grailsApplication?.config?.getProperty('grails.i18n.default.locale') ?:
                grailsApplication?.config?.getProperty('spring.web.locale')
        (configured ? StringUtils.parseLocale(configured.toString()) : null) ?: Locale.ENGLISH
    }

    /**
     * A helper tag for creating currency selects.<br/>
     *
     * eg. &lt;g:currencySelect name="myCurrency" value="${currency}" /&gt;
     *
     * @emptyTag
     *
     * @attr from The currency symbols to select from, defaults to the major ones if not specified
     * @attr value The currency value as the currency code. Defaults to the currency for the current Locale if not specified
     */
    def currencySelect(Map attrs, Closure body) {
        if (!attrs.from) {
            attrs.from = DEFAULT_CURRENCY_CODES
        }
        try {
            def currency = attrs.value ?: Currency.getInstance(RCU.getLocale(request))
            attrs.value = currency.currencyCode
        }
        catch (IllegalArgumentException iae) {
            attrs.value = null
        }
        // invoke generic select
        out << select(attrs)
    }

    /**
     * A helper tag for creating HTML selects.<br/>
     *
     * Examples:<br/>
     * &lt;g:select name="user.age" from="${18..65}" value="${age}" /&gt;<br/>
     * &lt;g:select name="user.company.id" from="${Company.list()}" value="${user?.company.id}" optionKey="id" /&gt;<br/>
     *
     * @emptyTag
     *
     * @attr name REQUIRED the select name
     * @attr id the DOM element id - uses the name attribute if not specified
     * @attr from REQUIRED The list or range to select from
     * @attr keys A list of values to be used for the value attribute of each "option" element.
     * @attr optionKey By default value attribute of each &lt;option&gt; element will be the result of a "toString()" call on each element. Setting this allows the value to be a bean property of each element in the list.
     * @attr optionValue By default the body of each &lt;option&gt; element will be the result of a "toString()" call on each element in the "from" attribute list. Setting this allows the value to be a bean property of each element in the list.
     * @attr value The current selected value that evaluates equals() to true for one of the elements in the from list.
     * @attr multiple boolean value indicating whether the select a multi-select (automatically true if the value is a collection, sets to single-select either if multiple is missing or it is explicitly set to false)
     * @attr valueMessagePrefix By default the value "option" element will be the result of a "toString()" call on each element in the "from" attribute list. Setting this allows the value to be resolved from the I18n messages. The valueMessagePrefix will be suffixed with a dot ('.') and then the value attribute of the option to resolve the message. If the message could not be resolved, the value is presented.
     * @attr noSelection A single-entry map detailing the key and value to use for the "no selection made" choice in the select box. If there is no current selection this will be shown as it is first in the list, and if submitted with this selected, the key that you provide will be submitted. Typically this will be blank - but you can also use 'null' in the case that you're passing the ID of an object
     * @attr disabled boolean value indicating whether the select is disabled or enabled (defaults to false - enabled)
     * @attr readonly boolean value indicating whether the select is read only or editable (defaults to false - editable)
     * @attr dataAttrs a Map that adds data-* attributes to the &lt;option&gt; elements. Map's keys will be used as names of the data-* attributes like so: data-${key} (i.e. with a "data-" prefix). The object belonging to a Map's key determines the value of the data-* attribute. It can be a string referring to a property of beans in {@code from}, a Closure that accepts an item from {@code from} and returns the value or a List that contains a value for each of the &lt;option&gt;s.
     * @attr locale The locale to use for formatting. Defaults to the current request locale and then the system default locale if not specified
     */
    def select(Map attrs) {
        if (!attrs.name) {
            throwTagError('Tag [select] is missing required attribute [name]')
        }
        if (!attrs.containsKey('from')) {
            throwTagError('Tag [select] is missing required attribute [from]')
        }
        def messageSource = grailsAttributes.getApplicationContext().getBean('messageSource')
        def locale = FormatTagLib.resolveLocale(attrs.remove('locale'))
        def writer = out
        def from = attrs.remove('from')
        def keys = attrs.remove('keys')
        def optionKey = attrs.remove('optionKey')
        def optionDisabled = attrs.remove('optionDisabled')
        def optionValue = attrs.remove('optionValue')
        def value = attrs.remove('value')
        def dataAttrs = attrs.remove('dataAttrs')
        if (value instanceof Collection && attrs.multiple == null) {
            attrs.multiple = 'multiple'
        }
        if (attrs.multiple == false) {
            attrs.remove('multiple')
        }
        if (value instanceof CharSequence) {
            value = value.toString()
        }
        def valueMessagePrefix = attrs.remove('valueMessagePrefix')
        def noSelection = attrs.remove('noSelection')
        if (noSelection != null) {
            noSelection = noSelection.entrySet().iterator().next()
        }
        booleanToAttribute(attrs, 'disabled')
        booleanToAttribute(attrs, 'readonly')

        writer << '<select '
        // process remaining attributes
        outputAttributes(attrs, writer, true)

        writer << '>'
        writer.println()

        if (noSelection) {
            renderNoSelectionOptionImpl(writer, noSelection.key, noSelection.value, value)
            writer.println()
        }

        // create options from list
        from.eachWithIndex { el, i ->
            def keyDisabled
            def keyValue
            def dataAttrsMap = getDataAttr(el, dataAttrs, i)
            writer << '<option '
            if (keys) {
                keyValue = keys[i]
                writeValueAndCheckIfSelected(attrs.name, keyValue, value, writer, dataAttrsMap)
            }
            else if (optionKey) {
                def keyValueObject
                if (optionKey instanceof Closure) {
                    keyValue = optionKey(el)
                }
                else if (el != null && optionKey == 'id' && grailsApplication.getArtefact(DomainClassArtefactHandler.TYPE, el.getClass().name)) {
                    keyValue = el.ident()
                    keyValueObject = el
                }
                else {
                    keyValue = el[optionKey]
                    keyValueObject = el
                }
                if (optionDisabled) {
                    if (optionDisabled instanceof Closure) {
                        keyDisabled = optionDisabled(el)
                    }
                    else {
                        keyDisabled = el[optionDisabled]
                    }
                }
                writeValueAndCheckIfSelected(attrs.name, keyValue, value, writer, dataAttrsMap, keyValueObject, keyDisabled)
            }
            else {
                keyValue = el
                writeValueAndCheckIfSelected(attrs.name, keyValue, value, writer, dataAttrsMap)
            }
            writer << '>'
            if (optionValue) {
                if (optionValue instanceof Closure) {
                    writer << optionValue(el).toString().encodeAsHTML()
                }
                else {
                    writer << el[optionValue].toString().encodeAsHTML()
                }
            }
            else if (el instanceof MessageSourceResolvable) {
                writer << messageSource.getMessage(el, locale)
            }
            else if (valueMessagePrefix) {
                def message = messageSource.getMessage("${valueMessagePrefix}.${keyValue}", null, null, locale)
                if (message != null) {
                    writer << message.encodeAsHTML()
                }
                else if (keyValue && keys) {
                    def s = el.toString()
                    if (s) writer << s.encodeAsHTML()
                }
                else if (keyValue) {
                    writer << keyValue.encodeAsHTML()
                }
                else {
                    def s = el.toString()
                    if (s) writer << s.encodeAsHTML()
                }
            }
            else {
                def s = el.toString()
                if (s) writer << s.encodeAsHTML()
            }
            writer << '</option>'
            writer.println()
        }
        // close tag
        writer << '</select>'
    }

    private void writeValueAndCheckIfSelected(selectName, keyValue, value, writer, dataAttrsMap) {
        writeValueAndCheckIfSelected(selectName, keyValue, value, writer, dataAttrsMap, null)
    }
    private void writeValueAndCheckIfSelected(selectName, keyValue, value, writer, dataAttrsMap, el) {
        writeValueAndCheckIfSelected(selectName, keyValue, value, writer, dataAttrsMap, el, null)
    }

    private void writeValueAndCheckIfSelected(selectName, keyValue, value, writer, dataAttrsMap, el, keyDisabled) {

        boolean selected = false
        def keyClass = keyValue?.getClass()
        if (keyClass.isInstance(value)) {
            selected = (keyValue == value)
        }
        else if (value instanceof Collection) {
            // first try keyValue
            selected = value.contains(keyValue)
            if (!selected && el != null) {
                selected = value.contains(el)
            }
        }
        // GRAILS-3596: Make use of Groovy truth to handle GString <-> String
        // and other equivalent types (such as numbers, Integer <-> Long etc.).
        else if (keyValue == value) {
            selected = true
        }
        else if (keyClass && value != null) {
            try {
                value = conversionService.convert(value, keyClass)
                selected = keyValue == value
            }
            catch (ignored) {
            }
        }
        keyValue = processFormFieldValueIfNecessary(selectName, "${keyValue}", 'option')
        writer << "value=\"${keyValue.toString().encodeAsHTML()}\" "

        if (dataAttrsMap) {
            dataAttrsMap.each { key, val ->
                writer << "data-${key.toString().encodeAsHTML()}=\"${val.toString().encodeAsHTML()}\""
            }
        }
        if (selected) {
            writer << 'selected="selected" '
        }
        if (keyDisabled && !selected) {
            writer << 'disabled="disabled" '
        }
    }

    private static Map getDataAttr(el, dataAttrs, index) {
        Map ret = [:]
        if (dataAttrs) {
            dataAttrs.each { k, v ->
                if (v instanceof CharSequence) {
                    //in case of bean property
                    ret[k] = el[v]
                } else if (v instanceof Closure) {
                    ret[k] = v(el)
                } else {
                    //in case of collection
                    ret[k] = v[index]
                }
            }
        }
        ret
    }

    /**
     * A helper tag for creating radio buttons.
     *
     * @emptyTag
     *
     * @attr value REQUIRED The value of the radio button
     * @attr name REQUIRED The name of the radio button
     * @attr checked boolean to indicate that the radio button should be checked
     * @attr disabled boolean to indicate that the radio button should be disabled
     * @attr readonly boolean to indicate that the radio button should not be editable
     * @attr id the DOM element id
     */
    def radio(Map attrs) {
        def value = attrs.remove('value')
        def name = attrs.remove('name')
        booleanToAttribute(attrs, 'disabled')
        booleanToAttribute(attrs, 'readonly')

        def checked = attrs.remove('checked') ? true : false
        value = processFormFieldValueIfNecessary(name, "${value?.toString()?.encodeAsHTML()}", 'radio')
        out << "<input type=\"radio\" name=\"${name}\"${ checked ? ' checked="checked" ' : ' '}value=\"${value?.toString()?.encodeAsHTML()}\" "
        if (!attrs.containsKey('id')) {
            out << """id="${name}" """
        }
        // process remaining attributes
        outputAttributes(attrs, out)

        // close the tag, with no body
        out << ' />'
    }

    /**
     * A helper tag for creating radio button groups.
     *
     * @attr name REQUIRED The name of the group
     * @attr values REQUIRED The list values for the radio buttons
     * @attr value The current selected value
     * @attr labels Labels for each value contained in the values list. If this is ommitted the label property on the iterator variable (see below) will default to 'Radio ' + value.
     * @attr disabled Disables the resulting radio buttons.
     * @attr readonly Makes the resulting radio buttons to not be editable
     */
    def radioGroup(Map attrs, Closure body) {
        def value = attrs.remove('value')
        def values = attrs.remove('values')
        def labels = attrs.remove('labels')
        def name = attrs.remove('name')
        booleanToAttribute(attrs, 'disabled')
        booleanToAttribute(attrs, 'readonly')

        values.eachWithIndex { val, idx ->
            def it = new Expando()
            def radioWriter = new FastStringWriter()
            radioWriter << "<input type=\"radio\" name=\"${name}\" "
            if (value?.toString().equals(val.toString())) {
                radioWriter << 'checked="checked" '
            }
            // Generate
            def processedVal = processFormFieldValueIfNecessary(name, val.toString().encodeAsHTML(), 'radio')
            radioWriter << "value=\"${processedVal}\" "

            // process remaining attributes
            outputAttributes(attrs, radioWriter)
            radioWriter << '/>'

            it.radio = raw(radioWriter.buffer)

            it.label = labels == null ? 'Radio ' + val : labels[idx]

            out << body(it)
            out.println()
        }
    }

    private def processFormFieldValueIfNecessary(name, value, type) {
        if (requestDataValueProcessor != null) {
            return requestDataValueProcessor.processFormFieldValue(request, name, "${value}", type)
        }
        return value
    }

    /**
     * Filters the url through the RequestDataValueProcessor bean if it is registered.
     */
    private String processedUrl(String link, request) {
        if (requestDataValueProcessor == null) {
            return link
        }

        return requestDataValueProcessor.processUrl(request, link)
    }

    @Override
    void setConfiguration(Config co) {
        // Some attributes can be treated as boolean, but must be converted to the
        // expected value.
        booleanAttributes = co.getProperty('grails.tags.booleanToAttributes', List, DEFAULT_BOOLEAN_ATTRIBUTES)
    }
}
