/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.datastore.gorm.jdbc

import java.util.regex.Matcher
import java.util.regex.Pattern

import groovy.transform.CompileStatic
import org.springframework.util.StringUtils

/**
 * Generates relaxed name variations from a given source.
 *
 * @author Phillip Webb
 * @author Dave Syer
 * @see RelaxedDataBinder
 */
@CompileStatic
final class RelaxedNames implements Iterable<String> {

    private static final Pattern CAMEL_CASE_PATTERN = Pattern.compile('([^A-Z-])([A-Z])')

    private static final Pattern SEPARATED_TO_CAMEL_CASE_PATTERN = Pattern.compile('[_\\-.]')

    private final String name

    private final Set<String> values = new LinkedHashSet<>()

    /**
     * Create a new {@link RelaxedNames} instance.
     * @param name the source name. For the maximum number of variations specify the name
     * using dashed notation (e.g. {@literal my-property-name}
     */
    RelaxedNames(String name) {
        this.name = (name == null ? '' : name)
        initialize(this.name, this.values)
    }

    @Override
    Iterator<String> iterator() {
        return this.values.iterator()
    }

    private void initialize(String name, Set<String> values) {
        if (values.contains(name)) {
            return
        }
        for (Variation variation : Variation.values()) {
            for (Manipulation manipulation : Manipulation.values()) {
                String result = name
                result = manipulation.apply(result)
                result = variation.apply(result)
                values.add(result)
                initialize(result, values)
            }
        }
    }

    /**
     * Name variations.
     */
    static enum Variation {

        NONE {
            @Override
            String apply(String value) {
                return value
            }
        },

        LOWERCASE {
            @Override
            String apply(String value) {
                return value.toLowerCase()
            }
        },

        UPPERCASE {
            @Override
            String apply(String value) {
                return value.toUpperCase()
            }
        }

        abstract String apply(String value)

    }

    /**
     * Name manipulations.
     */
    static enum Manipulation {

        NONE {
            @Override
            String apply(String value) {
                return value
            }
        },

        HYPHEN_TO_UNDERSCORE {
            @Override
            String apply(String value) {
                return value.replace('-', '_')
            }
        },

        UNDERSCORE_TO_PERIOD {
            @Override
            String apply(String value) {
                return value.replace('_', '.')
            }
        },

        PERIOD_TO_UNDERSCORE {
            @Override
            String apply(String value) {
                return value.replace('.', '_')
            }
        },

        CAMELCASE_TO_UNDERSCORE {
            @Override
            String apply(String value) {
                Matcher matcher = CAMEL_CASE_PATTERN.matcher(value)
                StringBuilder result = new StringBuilder()
                while (matcher.find()) {
                    matcher.appendReplacement(result, matcher.group(1) + '_' +
                            StringUtils.uncapitalize(matcher.group(2)))
                }
                matcher.appendTail(result)
                return result.toString()
            }
        },

        CAMELCASE_TO_HYPHEN {
            @Override
            String apply(String value) {
                Matcher matcher = CAMEL_CASE_PATTERN.matcher(value)
                StringBuilder result = new StringBuilder()
                while (matcher.find()) {
                    matcher.appendReplacement(result, matcher.group(1) + '-' +
                            StringUtils.uncapitalize(matcher.group(2)))
                }
                matcher.appendTail(result)
                return result.toString()
            }
        },

        SEPARATED_TO_CAMELCASE {
            @Override
            String apply(String value) {
                return separatedToCamelCase(value, false)
            }
        },

        CASE_INSENSITIVE_SEPARATED_TO_CAMELCASE {
            @Override
            String apply(String value) {
                return separatedToCamelCase(value, true)
            }
        }

        abstract String apply(String value)

        private static String separatedToCamelCase(String value, boolean caseInsensitive) {
            StringBuilder builder = new StringBuilder()
            for (String field : SEPARATED_TO_CAMEL_CASE_PATTERN.split(value)) {
                field = (caseInsensitive ? field.toLowerCase() : field)
                builder.append(
                        builder.length() == 0 ? field : StringUtils.capitalize(field))
            }
            for (String suffix : ['_', '-', '.'] as String[]) {
                if (value.endsWith(suffix)) {
                    builder.append(suffix)
                }
            }
            return builder.toString()
        }

    }

}
