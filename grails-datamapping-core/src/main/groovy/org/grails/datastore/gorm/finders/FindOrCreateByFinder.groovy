/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.datastore.gorm.finders

import java.util.regex.Pattern

import groovy.transform.CompileStatic

import org.springframework.core.convert.ConversionException

import org.grails.datastore.gorm.DatastoreResolver
import org.grails.datastore.mapping.core.Datastore
import org.grails.datastore.mapping.core.exceptions.ConfigurationException
import org.grails.datastore.mapping.model.MappingContext

/**
 * Finder used to return a single result
 */
@CompileStatic
class FindOrCreateByFinder extends AbstractFindByFinder {

    public static final String METHOD_PATTERN = '(findOrCreateBy)([A-Z]\\w*)'

    FindOrCreateByFinder(final String methodPattern, final Datastore datastore) {
        super(Pattern.compile(methodPattern), datastore)
    }

    FindOrCreateByFinder(final String methodPattern, DatastoreResolver datastoreResolver, MappingContext mappingContext) {
        super(Pattern.compile(methodPattern), AbstractFindByFinder.OPERATORS, datastoreResolver, mappingContext)
    }

    FindOrCreateByFinder(final Datastore datastore) {
        this(METHOD_PATTERN, datastore)
    }

    FindOrCreateByFinder(DatastoreResolver datastoreResolver, MappingContext mappingContext) {
        this(METHOD_PATTERN, datastoreResolver, mappingContext)
    }

    FindOrCreateByFinder(MappingContext mappingContext) {
        super(Pattern.compile(METHOD_PATTERN), mappingContext)
    }

    FindOrCreateByFinder(final String methodPattern, MappingContext mappingContext) {
        super(Pattern.compile(methodPattern), mappingContext)
    }

    @Override
    protected Object doInvokeInternal(final DynamicFinderInvocation invocation) {

        if (OPERATOR_OR.equals(invocation.getOperator())) {
            throw new MissingMethodException(invocation.getMethodName(), invocation.getJavaClass(), invocation.getArguments())
        }
        validateInvocation(invocation)

        Object result
        try {
            result = super.doInvokeInternal(invocation)
        } catch (ConversionException e) { // TODO this is not the right place to deal with this...
            throw new MissingMethodException(invocation.getMethodName(), invocation.getJavaClass(), invocation.getArguments())
        }
        if (result == null) {
            Map m = new HashMap()
            List<MethodExpression> expressions = invocation.getExpressions()
            for (MethodExpression me : expressions) {
                if (!(me instanceof MethodExpression.Equal)) {
                    throw new MissingMethodException(invocation.getMethodName(), invocation.getJavaClass(), invocation.getArguments())
                }
                String propertyName = me.propertyName
                Object[] arguments = me.getArguments()
                m.put(propertyName, arguments[0])
            }
            MetaClass metaClass = GroovySystem.getMetaClassRegistry().getMetaClass(invocation.getJavaClass())
            result = metaClass.invokeConstructor([m] as Object[])
            if (shouldSaveOnCreate()) {
                metaClass.invokeMethod(result, 'save', (Object[]) null)
            }
        }
        return result
    }

    protected void validateInvocation(DynamicFinderInvocation invocation) {
        for (MethodExpression methodExpression : invocation.getExpressions()) {
            if (methodExpression instanceof MethodExpression.GreaterThan ||
                    methodExpression instanceof MethodExpression.LessThan ||
                    methodExpression instanceof MethodExpression.GreaterThanEquals ||
                    methodExpression instanceof MethodExpression.LessThanEquals) {
                throw new ConfigurationException('Only equality-based expressions are supported for ' + invocation.getMethodName())
            }
        }
    }

    protected boolean shouldSaveOnCreate() {
        return false
    }
}
