apply plugin:'application'

mainClassName = "org.grails.cli.GrailsCli"

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.gradle.org/gradle/libs-releases'
        description = 'Needed for Gradle Tooling API'
    }
}

dependencies {
    // compile grails-shell with the Groovy version provided by Gradle
    // to ensure build compatibility with Gradle, currently Groovy 3.0.x
    // when used by grails-gradle-plugin
    // see: https://docs.gradle.org/current/userguide/compatibility.html#groovy
    compileOnly "org.codehaus.groovy:groovy:$GroovySystem.version"
    compileOnly "org.codehaus.groovy:groovy-templates:$GroovySystem.version"
    compileOnly "org.codehaus.groovy:groovy-xml:$GroovySystem.version"
    compileOnly "org.codehaus.groovy:groovy-ant:$GroovySystem.version"
    compileOnly "org.codehaus.groovy:groovy-json:$GroovySystem.version"

    runtimeOnly "org.apache.groovy:groovy-ant"
    runtimeOnly "org.apache.groovy:groovy-json"
    runtimeOnly "org.apache.groovy:groovy-jmx"
    runtimeOnly "org.apache.groovy:groovy-xml"

    api project(":grails-bootstrap")
    api project(":grails-gradle-model")
    api "org.apache.ant:ant"
    api "org.fusesource.jansi:jansi"
    api "jline:jline"

    compileOnly "org.gradle:gradle-tooling-api:$gradleToolingApiVersion"
    compileOnly "org.springframework.boot:spring-boot"
    compileOnly "org.springframework.boot:spring-boot-loader-tools"
    compileOnly "org.springframework:spring-web"
    api "org.springframework.boot:spring-boot-cli"
    compileOnly "jakarta.servlet:jakarta.servlet-api"
    implementation "org.apache.maven:maven-resolver-provider:$apacheMavenVersion"
    compileOnly "net.sf.jopt-simple:jopt-simple:$joptSimpleVersion"

    implementation("org.apache.maven:maven-settings-builder:$apacheMavenVersion") {
        exclude group: "javax.inject", module: "javax.inject"
    }

    implementation "org.sonatype.plexus:plexus-sec-dispatcher:$plexusSecDispatcherVersion"
    implementation("org.sonatype.sisu:sisu-inject-plexus:$plexusSisuInjectVersion") {
        exclude group: "javax.enterprise", module: "cdi-api"
        exclude group: "org.sonatype.sisu", module: "sisu-inject-bean"
    }

    implementation("org.apache.maven.resolver:maven-resolver-connector-basic:$apacheMavenResolverVersion")
    implementation("org.apache.maven.resolver:maven-resolver-impl:$apacheMavenResolverVersion")
    implementation("org.apache.maven.resolver:maven-resolver-transport-file:$apacheMavenResolverVersion")
    implementation("org.apache.maven.resolver:maven-resolver-transport-http:$apacheMavenResolverVersion") {
        exclude group: "org.slf4j", module:"jcl-over-slf4j"
        exclude group: "commons-codec", module:"commons-codec"
    }

    testImplementation "net.sf.expectit:expectit-core:$expectitCoreVersion"
    testImplementation "com.github.jnr:jnr-posix:3.1.20"
    testImplementation "org.apache.groovy:groovy-xml"
    testImplementation "org.apache.groovy:groovy-json"
    testImplementation "org.apache.groovy:groovy-templates"
    testImplementation "org.apache.groovy:groovy-ant"

    runtimeOnly "org.slf4j:slf4j-simple"
    runtimeOnly "org.codehaus.plexus:plexus-component-api:$plexusComponentApiVersion"
}

eclipse {
    classpath {
       file {
           whenMerged { classpath ->
                classpath.entries.find { entry -> entry.kind == 'src' && entry.path == "src/test/resources" }?.excludes=["profiles-repository/**", "gradle-sample/**"]
           }
       }
    }
}

apply from: "../gradle/integration-test.gradle"

integrationTest {
    // jline doesn't use jline.terminal system property when TERM is dumb so use different TERM value for tests
    // https://github.com/jline/jline2/blob/6a1b6bf/src/main/java/jline/TerminalFactory.java#L54-L57
    environment 'TERM', 'xterm'
    // execute in single thread
    maxParallelForks = 1
}
