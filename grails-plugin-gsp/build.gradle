sourceSets {
    ast {
        groovy.srcDirs = ['src/ast/groovy']
        resources.srcDirs = []
    }
}

configurations {
    compile.extendsFrom astRuntime
}

dependencies {
    compile sourceSets.ast.output,
            project(":grails-plugin-codecs"),
            project(":grails-logging"),
            project(":grails-web-gsp-taglib")

    astCompile project(":grails-web"),
               project(":grails-plugin-controllers")
}

test {
    maxParallelForks = isContinuousIntegrationBuild ? 2 : 4
    forkEvery = 100
    if(!isContinuousIntegrationBuild) {
      maxHeapSize = '1024m'
    }
}
jar{
    from compileAstGroovy.outputs.files
}

eclipse {
    classpath {
       file {
           whenMerged { classpath ->
                // remove invalid build/resources/ast entry
                classpath.entries.removeAll { entry -> entry?.kind == 'lib' && entry?.path ==~ ".+/build/\\w+/ast\$" }
           }
       }
    }
}