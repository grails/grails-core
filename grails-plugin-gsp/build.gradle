
sourceSets {
    ast {
        groovy.srcDirs = ['src/ast/groovy']
        resources.srcDirs = []
    }
}

configurations {
    compile.extendsFrom astRuntime
}

dependencies {
    compile "commons-lang:commons-lang:$commonsLangVersion",
            sourceSets.ast.output,
            project(":grails-plugin-codecs"),
            project(":grails-logging")

    astCompile project(":grails-web"),
               project(":grails-plugin-controllers")
}

test {
    maxParallelForks = 4
    forkEvery = 100
    maxHeapSize = '1024m'
}
jar{
    from compileAstGroovy.outputs.files
}

jar.appendix = 'plugin-gsp'

eclipse {
    classpath {
       file {        
           whenMerged { classpath ->       
                // remove invalid build/resources/ast entry
                classpath.entries.removeAll { entry -> entry?.kind == 'lib' && entry?.path?.endsWith('build/resources/ast') }
           }
       }
    }
}