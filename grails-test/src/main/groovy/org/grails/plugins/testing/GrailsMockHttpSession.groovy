/*
 * Copyright 2024 original authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package org.grails.plugins.testing

import org.springframework.mock.web.MockHttpSession

/**
 * Simple sub-class of Spring's MockHttpSession that adds support for
 * map and property notation, i.e. "session['attr']" and "session.attr".
 */
class GrailsMockHttpSession extends MockHttpSession {

    Object getProperty(String name) {
        def mp = getClass().metaClass.getMetaProperty(name)
        return mp ? mp.getProperty(this) : getAttribute(name)
    }

    void setProperty(String name, Object value) {
        def mp = getClass().metaClass.getMetaProperty(name)
        if (mp) {
            mp.setProperty(this, value)
        }
        else {
            setAttribute(name, value)
        }
    }

    Object getAt(String name) {
        return super.getAttribute(name)
    }

    void putAt(String name, Object value) {
        super.setAttribute(name, value)
    }
}
