/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */
package org.grails.orm.hibernate

import groovy.transform.CompileStatic
import jakarta.transaction.Status
import jakarta.transaction.Transaction
import jakarta.transaction.TransactionManager
import org.hibernate.FlushMode
import org.hibernate.HibernateException
import org.hibernate.Session
import org.hibernate.context.spi.CurrentSessionContext
import org.hibernate.engine.spi.SessionFactoryImplementor
import org.hibernate.engine.transaction.jta.platform.spi.JtaPlatform
import org.hibernate.service.spi.ServiceBinding
import org.slf4j.Logger
import org.slf4j.LoggerFactory

import org.springframework.dao.DataAccessResourceFailureException
import org.springframework.transaction.jta.SpringJtaSynchronizationAdapter
import org.springframework.transaction.support.TransactionSynchronization
import org.springframework.transaction.support.TransactionSynchronizationManager

import org.grails.orm.hibernate.support.hibernate7.SessionHolder
import org.grails.orm.hibernate.support.hibernate7.SpringFlushSynchronization
import org.grails.orm.hibernate.support.hibernate7.SpringJtaSessionContext
import org.grails.orm.hibernate.support.hibernate7.SpringSessionSynchronization

/**
 * Based on org.springframework.orm.hibernate4.SpringSessionContext.
 *
 * @author Juergen Hoeller
 * @author Burt Beckwith
 */
@CompileStatic
class GrailsSessionContext implements CurrentSessionContext {

    @Serial
    private static final long serialVersionUID = 1

    private static final Logger LOG = LoggerFactory.getLogger(GrailsSessionContext)

    protected final SessionFactoryImplementor sessionFactory
    protected CurrentSessionContext jtaSessionContext

    // Match Spring's default: currentSession() must not create a Session outside an active transaction.
    protected boolean allowCreate = false

    /**
     * Constructor.
     *
     * @param sessionFactory the SessionFactory to provide current Sessions for
     */
    GrailsSessionContext(SessionFactoryImplementor sessionFactory) {
        this.sessionFactory = sessionFactory
    }

    void initJta() {
        TransactionManager tm = resolveJtaTransactionManager()
        jtaSessionContext = tm == null ? null : buildJtaSessionContext()
    }

    /**
     * Resolves the JTA {@link TransactionManager} from the session factory's service registry.
     * Protected to allow overriding in tests without a real JTA platform.
     */
    protected TransactionManager resolveJtaTransactionManager() {
        JtaPlatform jtaPlatform = sessionFactory.serviceRegistry.getService(JtaPlatform)
        return jtaPlatform != null ? jtaPlatform.retrieveTransactionManager() : null
    }

    /**
     * Creates the JTA-backed {@link CurrentSessionContext}.
     * Protected to allow overriding in tests without a real JTA platform.
     */
    protected CurrentSessionContext buildJtaSessionContext() {
        return new SpringJtaSessionContext(sessionFactory)
    }

    /** Retrieve the Spring-managed Session for the current thread, if any. */
    @Override
    Session currentSession() throws HibernateException {
        Object value = TransactionSynchronizationManager.getResource(sessionFactory)
        if (value instanceof Session) {
            return (Session) value
        }

        if (value instanceof SessionHolder) {
            SessionHolder sessionHolder = (SessionHolder) value
            Session session = sessionHolder.session
            if (TransactionSynchronizationManager.isSynchronizationActive() &&
                    !sessionHolder.isSynchronizedWithTransaction()) {
                TransactionSynchronizationManager.registerSynchronization(
                        createSpringSessionSynchronization(sessionHolder))
                sessionHolder.synchronizedWithTransaction = true
                // Switch to FlushMode.AUTO, as we have to assume a thread-bound Session
                // with FlushMode.MANUAL, which needs to allow flushing within the transaction.
                FlushMode flushMode = session.hibernateFlushMode
                if (flushMode == FlushMode.MANUAL &&
                        !TransactionSynchronizationManager.isCurrentTransactionReadOnly()) {
                    session.setHibernateFlushMode(FlushMode.AUTO)
                    sessionHolder.previousFlushMode = flushMode
                }
            }
            return session
        }

        if (jtaSessionContext != null) {
            Session session = jtaSessionContext.currentSession()
            if (TransactionSynchronizationManager.isSynchronizationActive()) {
                TransactionSynchronizationManager.registerSynchronization(createSpringFlushSynchronization(session))
            }
            return session
        }

        if (allowCreate) {
            // be consistent with older HibernateTemplate behavior
            return createSession(value)
        }

        throw new HibernateException('No Session found for current thread')
    }

    private Session createSession(Object resource) {
        LOG.debug('Opening Hibernate Session')

        SessionHolder sessionHolder = (SessionHolder) resource

        Session session = sessionFactory.openSession()

        // Use same Session for further Hibernate actions within the transaction.
        // Thread object will get removed by synchronization at transaction completion.
        if (TransactionSynchronizationManager.isSynchronizationActive()) {
            // We're within a Spring-managed transaction, possibly from JtaTransactionManager.
            LOG.debug('Registering Spring transaction synchronization for new Hibernate Session')
            SessionHolder holderToUse = sessionHolder
            if (holderToUse == null) {
                holderToUse = new SessionHolder(session)
            }
            if (TransactionSynchronizationManager.isCurrentTransactionReadOnly()) {
                session.setHibernateFlushMode(FlushMode.MANUAL)
            }
            TransactionSynchronizationManager.registerSynchronization(createSpringSessionSynchronization(holderToUse))
            holderToUse.synchronizedWithTransaction = true
            if (sessionHolder == null) {
                TransactionSynchronizationManager.bindResource(sessionFactory, holderToUse)
            }
        } else {
            // No Spring transaction management active -> try JTA transaction synchronization.
            registerJtaSynchronization(session, sessionHolder)
        }
        return session
    }

    protected void registerJtaSynchronization(Session session, SessionHolder sessionHolder) {

        // JTA synchronization is only possible with a jakarta.transaction.TransactionManager.
        // We'll check the Hibernate SessionFactory: If a TransactionManagerLookup is specified
        // in Hibernate configuration, it will contain a TransactionManager reference.
        TransactionManager jtaTm = lookupJtaTransactionManager(this.sessionFactory)
        if (jtaTm == null) {
            return
        }

        try {
            Transaction jtaTx = jtaTm.transaction
            if (jtaTx == null) {
                return
            }

            int jtaStatus = jtaTx.status
            if (jtaStatus != Status.STATUS_ACTIVE && jtaStatus != Status.STATUS_MARKED_ROLLBACK) {
                return
            }

            LOG.debug('Registering JTA transaction synchronization for new Hibernate Session')
            SessionHolder holderToUse = sessionHolder
            // Register JTA Transaction with existing SessionHolder.
            // Create a new SessionHolder if none existed before.
            if (holderToUse == null) {
                holderToUse = new SessionHolder(session)
            }
            jtaTx.registerSynchronization(
                    new SpringJtaSynchronizationAdapter(createSpringSessionSynchronization(holderToUse)))
            holderToUse.synchronizedWithTransaction = true
            if (sessionHolder == null) {
                TransactionSynchronizationManager.bindResource(sessionFactory, holderToUse)
            }
        } catch (Exception ex) {
            throw new DataAccessResourceFailureException(
                    'Could not register synchronization with JTA TransactionManager', ex)
        }
    }

    /**
     * Looks up the JTA {@link TransactionManager} from the given session factory's service registry.
     * Protected to allow overriding in tests without a real JTA platform binding.
     */
    protected TransactionManager lookupJtaTransactionManager(SessionFactoryImplementor sf) {
        ServiceBinding<JtaPlatform> sb = sf.serviceRegistry.locateServiceBinding(JtaPlatform)
        if (sb == null || sb.service == null) {
            return null
        }
        return sb.service.retrieveTransactionManager()
    }

    protected TransactionSynchronization createSpringFlushSynchronization(Session session) {
        return new SpringFlushSynchronization(session)
    }

    protected TransactionSynchronization createSpringSessionSynchronization(SessionHolder sessionHolder) {
        return new SpringSessionSynchronization(sessionHolder, sessionFactory)
    }

}
